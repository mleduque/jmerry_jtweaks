BACKUP ~weidu_external/backup/jtweaks~
SUPPORT ~jmerry on the Beamdog forums and the Gibberlings 3 forums~

AUTO_EVAL_STRINGS

ALWAYS
	ACTION_IF ((FILE_EXISTS ~dlc/sod-dlc.zip~) OR (FILE_EXISTS ~sod-dlc.zip~)) THEN BEGIN FAIL @15 END // Modmerge check
	ACTION_IF NOT GAME_IS ~bgee bg2ee eet~ THEN BEGIN FAIL @10 END // EE games only
	INCLUDE ~jtweaks/resource/functions.tph~
	INCLUDE ~jtweaks/resource/jtweaks-ini.tph~
END

VERSION ~2.1~

README ~jtweaks/readme-jtweaks.txt~

LANGUAGE 
	~English~
	~english~
	~jtweaks/languages/english/JTweaks.tra~

//
//
// Wilson's strength doesn't stack indefinitely
//
//

BEGIN @1000 DESIGNATED 10
GROUP @1
	REQUIRE_PREDICATE GAME_IS ~bg2ee eet~ @11
	COPY_EXISTING "ohrbear6.SPL" ~override~
		LPF ALTER_SPELL_EFFECT INT_VAR match_opcode=44 timing=9 END
	BUT_ONLY // Corrected timing mode on Wilson's strength boost.

//
//
// Vernus can be raised
//
//

BEGIN @2000 DESIGNATED 20
GROUP @1
REQUIRE_PREDICATE GAME_IS ~bg2ee eet~ @11
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME ~#rdremov.spl~ @12
	COPY_EXISTING ~SPPR504.SPL~ ~override~
	              ~SPPR550.SPL~ ~override~
	              ~SPPR712.SPL~ ~override~
	              ~SPJA01.SPL~ ~override~
	              ~BHAAL4A.SPL~ ~override~
		LPF ADD_SPELL_EFFECT INT_VAR opcode=309 target=2 parameter1=1 duration=60 insert_point=0 STR_VAR resource=ohdress END
	BUT_ONLY // Add variable-setting effect to resurrection spells
	COPY_EXISTING ~RODS03.ITM~ ~override~
		LPF ADD_ITEM_EFFECT INT_VAR opcode=309 target=2 parameter1=1 duration=60 insert_point=0 STR_VAR resource=ohdress END
	BUT_ONLY // And the rod of resurrection
	COPY_EXISTING ~SPPR504A.SPL~ ~override~
	              ~SPPR550A.SPL~ ~override~
	              ~SPPR712A.SPL~ ~override~
	              ~SPJA01A.SPL~ ~override~
	              ~BHAAL4AA.SPL~ ~override~
		LPF DELETE_EFFECT INT_VAR match_opcode=309 STR_VAR match_resource=ohdress END
	BUT_ONLY // Remove variable-setting effect from resurrection subspells that can't affect Vernus

	ACTION_IF FILE_EXISTS_IN_GAME ~SPPR712H.SPL~ THEN BEGIN
		COPY_EXISTING ~SPPR712H.SPL~ ~override~
			LPF DELETE_EFFECT INT_VAR match_opcode=309 STR_VAR match_resource=ohdress END
		BUT_ONLY
	END // Compatibility with resurrection healing component
	ACTION_IF FILE_EXISTS_IN_GAME ~BHAAL4AH.SPL~ THEN BEGIN
		COPY_EXISTING ~BHAAL4AH.SPL~ ~override~
			LPF DELETE_EFFECT INT_VAR match_opcode=309 STR_VAR match_resource=ohdress END
		BUT_ONLY
	END // Compatibility with resurrection healing component

//
//
// Fix Black Pits oversights
//
//

BEGIN @3000 DESIGNATED 30
GROUP @1
	REQUIRE_PREDICATE GAME_IS ~bgee eet~ @13
	COPY_EXISTING ~ITEMEXCL.2DA~ ~override~
		COUNT_2DA_ROWS 2 rowcount
		INSERT_2DA_ROW rowcount 2 ~CLCK31 1~
		UNLESS ~CLCK31~ // Only if it's not already there
	BUT_ONLY // Add Improved Cloak of Protection +2 to item exclusion list
	EXTEND_TOP ~BPPLOT.BCS~ ~jtweaks/resource/BPPlotPatch.baf~ // BP script removes itself if not in BP area
	COPY_EXISTING ~BPGIAFIR.BCS~ ~override~
		DECOMPILE_AND_PATCH BEGIN
			REPLACE_TEXTUALLY ~\(SetGlobalTimer("GIANT_POTION","LOCALS",ONE_ROUND)[%TAB% %LNL%%MNL%%WNL%]+\)UseItem("POTN27",NearestEnemyOf(Myself))~ ~\1 UseItem("POTN52",Myself)~
		END
	BUT_ONLY // Hogarl uses the right potion when he's hurt
	COPY_EXISTING ~BPXITH03.STO~ ~override~
		LPF REPLACE_STORE_ITEM INT_VAR number_in_stock=1 charges1=8 charges2=0 charges3=0 STR_VAR old_item=RING05 new_item=RING05 END
	BUT_ONLY // Correct number of charges on the Sandthief's Ring at tier 3

//
//
// Enchant Weapon works in contingencies
//
//

BEGIN @4000 DESIGNATED 40
GROUP @1
	COPY_EXISTING ~SPWI417.2DA~ ~override~
		COUNT_2DA_ROWS 3 rowcount
		PATCH_IF (rowcount > 0) THEN BEGIN
			SET_2DA_ENTRY 0 0 3 ~*~
			SET_2DA_ENTRY 0 1 3 ~SPWI417~
			SET_2DA_ENTRY 0 2 3 3
		END
		FOR (row = 1; row < rowcount; ++row) BEGIN
			REMOVE_2DA_ROW 1 3
		END // Index of remaining rows changes when operation performed.
	BUT_ONLY // Make one good row, clear all others

//
//
// Joinable NPCs don't have null kits
//
//

BEGIN @5000 DESIGNATED 50
GROUP @1
	COPY_EXISTING_REGEXP GLOB ~^.+\.CRE$~ ~override~
		READ_LONG 0x1cc joinable ELSE 0
		READ_LONG 0x244 kit
		READ_BYTE 0x273 class
		PATCH_IF ((joinable > 0) AND (kit = 0) AND (class > 0) AND (class < 22)) BEGIN
			WRITE_LONG 0x244 0x40000000
		END // New kit: Base Class/Generalist Mage.
	BUT_ONLY // Spurious matches may result; they're harmless.

//
//
// NPCs don't go in inaccessible locations
//
//

BEGIN @6000 DESIGNATED 60
GROUP @1
	REQUIRE_PREDICATE GAME_IS ~bg2ee eet~ @11
	COPY_EXISTING ~AR0800.ARE~ ~override~ // Graveyard District, Stein
		LPF ALTER_AREA_ACTOR INT_VAR y_coord=1071 STR_VAR actor_name=Stein END
	BUT_ONLY
	COPY_EXISTING ~AR1000.ARE~ ~override~ // Government District, Amnish Soldier
		READ_LONG 0x54 ofsActors		READ_SHORT 0x58 numActors		FOR (idx = 0; idx < numActors; ++idx) BEGIN			SET offsetx = ofsActors + (idx * 0x110) + 0x20
			SET offsety = ofsActors + (idx * 0x110) + 0x22			READ_SHORT offsetx xpos
			READ_SHORT offsety ypos			PATCH_IF ((xpos = 2796) AND (ypos = 538)) BEGIN
				WRITE_SHORT offsety 558
			END
		END // Multiple actors with same name, long version needed
	BUT_ONLY
	COMPILE ~jtweaks/resource/aeriemove.d~ // Circus Tent, Aerie after dialogue actions

//
//
// Close polymorph immunity loopholes
//
//

BEGIN @7000 DESIGNATED 70
GROUP @1
	OUTER_SET WandSubspell = 0
	OUTER_SET BoltSubspell = 0
	OUTER_SET SphereSubspell = 0

	ACTION_IF FILE_EXISTS_IN_GAME ~WAND09.SPL~ THEN BEGIN
		OUTER_SET WandSubspell = 1
		ACTION_IF (debug_messages) BEGIN
			PRINT ~Wand of Polymorphing subspell already exists. Skipping.~
		END
	END ELSE BEGIN
		COPY_EXISTING ~SPWI415.SPL~ ~override/WAND09.SPL~
			LPF DELETE_EFFECT END
			LPF ALTER_SPELL_HEADER INT_VAR projectile=1 END
			WRITE_LONG 0x8 0xFFFFFFFF // No name
		BUT_ONLY // Copy Polymorph Other as template, remove effects
		COPY_EXISTING ~WAND09.ITM~ ~override~ // Wand of Polymorphing
			LPF ITEM_EFFECT_TO_SPELL INT_VAR type=3 header=0 STR_VAR new_itm_spl=WAND09.SPL END
			LPF DELETE_EFFECT END // Move effects to subspell
			LPF ADD_ITEM_EFFECT INT_VAR opcode=146 target=2 power=0 timing=1 parameter1=10 parameter2=2 resist_dispel=0 duration=0 probability1=100 probability2=0 dicenumber=0 dicesize=0 savingthrow=0 savebonus=0 special=0 type=3 STR_VAR resource=WAND09 END
		BUT_ONLY // New "cast spell" effect, so specific immunity can work
	END

	ACTION_IF FILE_EXISTS_IN_GAME ~BOLT05.SPL~ THEN BEGIN
		OUTER_SET BoltSubspell = 1
		ACTION_IF (debug_messages) BEGIN
			PRINT ~Bolt of Polymorphing subspell already exists. Skipping.~
		END
	END ELSE BEGIN
		COPY_EXISTING ~SPWI415.SPL~ ~override/BOLT05.SPL~
			LPF DELETE_EFFECT END
			LPF ALTER_SPELL_HEADER INT_VAR projectile=1 END
			WRITE_LONG 0x8 0xFFFFFFFF
		BUT_ONLY// Copy Polymorph Other as template, remove effects
		COPY_EXISTING ~BOLT05.ITM~ ~override~ // Bolt of Polymorphing
			LPF ITEM_EFFECT_TO_SPELL INT_VAR type=2 header=0 STR_VAR new_itm_spl=BOLT05.ITM END
			LPF DELETE_EFFECT END // Move effects to subspell
			LPF ADD_ITEM_EFFECT INT_VAR opcode=146 target=2 power=0 timing=1 parameter1=10 parameter2=2 resist_dispel=0 duration=0 probability1=100 probability2=0 dicenumber=0 dicesize=0 savingthrow=0 savebonus=0 special=0 type=2 STR_VAR resource=BOLT05 END
		BUT_ONLY // New "cast spell" effect, so specific immunity can work
	END

	ACTION_IF FILE_EXISTS_IN_GAME ~SPWI711P.SPL~ THEN BEGIN
		OUTER_SET SphereSubspell = 1
		ACTION_IF (debug_messages) BEGIN
			PRINT ~Sphere of Chaos polymorph subspell already exists. Skipping.~
		END
	END ELSE BEGIN
		COPY_EXISTING ~SPWI711.SPL~ ~override/SPWI711P.SPL~
			LPF DELETE_EFFECT INT_VAR match_probability1=20 END
			LPF DELETE_EFFECT INT_VAR match_probability1=30 END
			LPF DELETE_EFFECT INT_VAR match_probability1=40 END
			LPF DELETE_EFFECT INT_VAR match_probability1=50 END
			LPF DELETE_EFFECT INT_VAR match_probability1=60 END
			LPF DELETE_EFFECT INT_VAR match_probability1=70 END
			LPF DELETE_EFFECT INT_VAR match_probability1=80 END
			LPF DELETE_EFFECT INT_VAR match_probability1=90 END
			LPF DELETE_EFFECT INT_VAR match_probability1=100 END
			LPF ALTER_SPELL_EFFECT INT_VAR probability1=100 power=0 END
			LPF ALTER_SPELL_HEADER INT_VAR projectile=1 END // No projectile
			WRITE_LONG 0x8 0xFFFFFFFF // No name
		BUT_ONLY // New subspell with only the polymorph effect
		COPY_EXISTING ~SPWI711.SPL~ ~override~ // Sphere of Chaos
			LPF DELETE_EFFECT INT_VAR match_probability1=10 END
			LPF ADD_SPELL_EFFECT INT_VAR opcode=146 target=2 power=7 timing=1 parameter1=0 parameter2=1 resist_dispel=0 duration=0 probability1=10 probability2=0 dicenumber=0 dicesize=0 savingthrow=0 savebonus=0 special=0 insert_point=0 STR_VAR resource=SPWI711P END
		BUT_ONLY // Replace polymorph effect with "cast spell" effect
	END

	COPY_EXISTING_REGEXP GLOB ".*\.ITM" ~override~ ".*\.SPL" ~override~
		PATCH_IF (WandSubspell = 0) BEGIN
			LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=206 STR_VAR match_resource=SPWI415 resource=WAND09 END // Specific immunity to wand polymorphs
		END
		PATCH_IF (BoltSubspell = 0) BEGIN
			LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=206 STR_VAR match_resource=SPWI415 resource=BOLT05 END // Specific immunity to bolt polymorphs
		END
		LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=206 STR_VAR match_resource=SPWM113 resource=SPWM183 END // Missing wild surge option
		PATCH_IF (SphereSubspell = 0) BEGIN
			LPF ALTER_EFFECT INT_VAR silent=1 match_opcode=206 STR_VAR match_resource=SPWI711 resource=SPWI711P END // Specific immunity to sphere polymorphs
		END
		IF ~SPWI415~ // Wrapper: skip unless Polymorph Other is referenced
	BUT_ONLY

//
//
// Shapeshift Corrections
//
//

BEGIN @8100 DESIGNATED 81 // Both druid and mage forms
SUBCOMPONENT @8000
GROUP @1
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME ~#rdremov.SPL~ @12
	ACTION_IF GAME_IS ~bgee~ THEN BEGIN // BGEE changes
		COPY_EXISTING ~SPCL612.SPL~ ~override~ // Druid wolf form
			LPF ALTER_EFFECT INT_VAR match_opcode=135 STR_VAR match_resource=WOLFCHAR resource=PLYWOLF END
		BUT_ONLY // Use the right polymorph target
		COPY_EXISTING ~WOLFM.ITM~ ~override~
			LPF ALTER_ITEM_HEADER INT_VAR header_type=1 damage_bonus=1 END
		BUT_ONLY // Add +1 damage for consistency with mage wolf form.

		COPY_EXISTING ~brbrp1.ITM~ ~override~ // Greater Werewolf form
			LPF ADD_ITEM_EQEFFECT INT_VAR opcode=98 target=1 parameter1=2 parameter2=3 timing=2 END
			LPF ADD_ITEM_EQEFFECT INT_VAR opcode=142 target=1 parameter1=0 parameter2=87 timing=2 END
		BUT_ONLY // Add regeneration as in BG2EE. Not relevant in standard rules.

		COPY_EXISTING ~SPCL611.SPL~ ~override~ // Description changes
		              ~SPWI497.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8010
			SAY DESC @8010
		BUT_ONLY // Brown bear shapeshifts
		COPY_EXISTING ~SPCL612.SPL~ ~override~
		              ~SPWI499.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8011
			SAY DESC @8011
		BUT_ONLY // Wolf shapeshifts
		COPY_EXISTING ~SPCL613.SPL~ ~override~
		              ~SPWI498.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8012
			SAY DESC @8012
		BUT_ONLY // Black bear shapeshifts
		COPY_EXISTING ~SPCL632.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8013
			SAY DESC @8013
		BUT_ONLY // Sword spider shapeshift
		COPY_EXISTING ~SPCL633.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8014
			SAY DESC @8014
		BUT_ONLY // Baby wyvern shapeshift
		COPY_EXISTING ~SPCL634.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8015
			SAY DESC @8015
		BUT_ONLY // Fire salamander shapeshift
		COPY_EXISTING ~SPCL643.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8016
			SAY DESC @8016
		BUT_ONLY // Werewolf shapeshift
		COPY_EXISTING ~SPCL644.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8017
			SAY DESC @8017
		BUT_ONLY // Greater werewolf shapeshift
		COPY_EXISTING ~SPWI494.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8021
			SAY DESC @8021
		BUT_ONLY // Ogre shapeshift
		COPY_EXISTING ~SPWI495.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8022
			SAY DESC @8022
		BUT_ONLY // Giant spider shapeshift
		// No high-level druid forms exist. Skip Shapechange forms.

		COPY_EXISTING ~PLYBEAR1.ITM~ ~override~
		              ~PLYBEAR2.ITM~ ~override~
		              ~WOLFM.ITM~ ~override~
		              ~PLYSPID2.ITM~ ~override~
		              ~PLYWYVRN.ITM~ ~override~
		              ~PLYSALA.ITM~ ~override~
		              ~BRBRP.ITM~ ~override~
		              ~BRBRP1.ITM~ ~override~
		              ~PLYFLIND.ITM~ ~override~
		              ~PLYMSTAR.ITM~ ~override~
		              ~PLYSPID.ITM~ ~override~
		              ~PLYJELLY.ITM~ ~override~
		              ~PLYWOLF1.ITM~ ~override~
			WRITE_LONG 0x18 (THIS BOR BIT13)
		BUT_ONLY // Add "forbid off-hand weapon" flag

	END ELSE BEGIN // BGEE and BG2EE polymorph handling differs; split.
		ACTION_CLEAR_ARRAY Imm2Effs
		ACTION_CLEAR_ARRAY Imm3Effs
		ACTION_CLEAR_ARRAY IronGolEffs

		COPY_EXISTING ~PLYBEAR1.ITM~ ~override~ // Bear shapeshifts
			LPF ALTER_EFFECT INT_VAR match_opcode=135 STR_VAR match_resource=BEARBR resource=BEARBL END
		BUT_ONLY
		COPY_EXISTING ~plybear2.ITM~ ~override~
			LPF ALTER_EFFECT INT_VAR match_opcode=135 STR_VAR match_resource=BEARBL resource=BEARBR END
		BUT_ONLY // Swap black and brown bears

		COPY_EXISTING ~WOLFM.ITM~ ~override~ // Druid wolf shapeshift
			LPF ALTER_EFFECT INT_VAR match_opcode=135 STR_VAR match_resource=WOLFCHAR resource=PLYWOLF END
			LPF ALTER_ITEM_HEADER INT_VAR header_type=1 damage_bonus=1 END
		BUT_ONLY // Switch wolf, add +1 damage

		COPY_EXISTING ~PLYMSTAR.ITM~ ~override~ // Ogre shapeshift
			READ_LONG 0x18 flags
			WRITE_LONG 0x18 (flags BOR BIT6)
		BUT_ONLY // Weapon becomes magical

		COPY_EXISTING ~SPWI498.SPL~ ~override~ // Mage black bear shapeshift
			LPF ALTER_EFFECT INT_VAR match_opcode=111 STR_VAR match_resource=PLYBEAR2 resource=PLYBEAR1 END
		BUT_ONLY // Use a different bear from SPWI497 brown bear shapeshift

		COPY_EXISTING ~IMMUNE2.ITM~ ~override~ // Immunity to weapons up to +1
			READ_LONG 0x6A ofsEf
			READ_SHORT 0x70 numEf
			FOR (idx = 0; idx < numEf; ++idx) BEGIN
				READ_SHORT (ofsEf + (0x30 * idx)) opcode
				READ_BYTE (ofsEf + 0x2 + (0x30 * idx)) target
				READ_BYTE (ofsEf + 0x3 + (0x30 * idx)) power
				READ_LONG (ofsEf + 0x4 + (0x30 * idx)) parameter1
				READ_LONG (ofsEf + 0x8 + (0x30 * idx)) parameter2
				READ_BYTE (ofsEf + 0xC + (0x30 * idx)) timing
				READ_BYTE (ofsEf + 0xD + (0x30 * idx)) resist_dispel
				READ_LONG (ofsEf + 0xE + (0x30 * idx)) duration
				READ_BYTE (ofsEf + 0x12 + (0x30 * idx)) probability1
				READ_BYTE (ofsEf + 0x13 + (0x30 * idx)) probability2
				READ_ASCII (ofsEf + 0x14 + (0x30 * idx)) resource
				READ_LONG (ofsEf + 0x1C + (0x30 * idx)) dicenumber
				READ_LONG (ofsEf + 0x20 + (0x30 * idx)) dicesize
				READ_LONG (ofsEf + 0x24 + (0x30 * idx)) savingthrow
				READ_LONG (ofsEf + 0x28 + (0x30 * idx)) savebonus
				READ_LONG (ofsEf + 0x2c + (0x30 * idx)) special
				SPRINT $Imm2Effs(~%opcode%~ ~%target%~ ~%power%~ ~%parameter1%~ ~%parameter2%~ ~%timing%~ ~%resist_dispel%~ ~%duration%~ ~%probability1%~ ~%probability2%~ ~%resource%~ ~%dicenumber%~ ~%dicesize%~ ~%savingthrow%~ ~%savebonus%~ ~%special%~) ~%idx%~
			END
		BUT_ONLY // Copy all effects to an array for later use.

		COPY_EXISTING ~IMMUNE3.ITM~ ~override~ // Immunity to weapons up to +2
			READ_LONG 0x6A ofsEf
			READ_SHORT 0x70 numEf
			FOR (idx = 0; idx < numEf; ++idx) BEGIN
				READ_SHORT (ofsEf + (0x30 * idx)) opcode
				READ_BYTE (ofsEf + 0x2 + (0x30 * idx)) target
				READ_BYTE (ofsEf + 0x3 + (0x30 * idx)) power
				READ_LONG (ofsEf + 0x4 + (0x30 * idx)) parameter1
				READ_LONG (ofsEf + 0x8 + (0x30 * idx)) parameter2
				READ_BYTE (ofsEf + 0xC + (0x30 * idx)) timing
				READ_BYTE (ofsEf + 0xD + (0x30 * idx)) resist_dispel
				READ_LONG (ofsEf + 0xE + (0x30 * idx)) duration
				READ_BYTE (ofsEf + 0x12 + (0x30 * idx)) probability1
				READ_BYTE (ofsEf + 0x13 + (0x30 * idx)) probability2
				READ_ASCII (ofsEf + 0x14 + (0x30 * idx)) resource
				READ_LONG (ofsEf + 0x1C + (0x30 * idx)) dicenumber
				READ_LONG (ofsEf + 0x20 + (0x30 * idx)) dicesize
				READ_LONG (ofsEf + 0x24 + (0x30 * idx)) savingthrow
				READ_LONG (ofsEf + 0x28 + (0x30 * idx)) savebonus
				READ_LONG (ofsEf + 0x2c + (0x30 * idx)) special
				SPRINT $Imm3Effs(~%opcode%~ ~%target%~ ~%power%~ ~%parameter1%~ ~%parameter2%~ ~%timing%~ ~%resist_dispel%~ ~%duration%~ ~%probability1%~ ~%probability2%~ ~%resource%~ ~%dicenumber%~ ~%dicesize%~ ~%savingthrow%~ ~%savebonus%~ ~%special%~) ~%idx%~
			END
		BUT_ONLY // Copy all effects to an array for later use.

		COPY_EXISTING ~AMUL22.ITM~ ~override~ // Iron golem immunity effects
		              ~CLCK30.ITM~ ~override~ // This is a serious hack job.
		              ~HELM06.ITM~ ~override~ // Combine four items and
		              ~TROLLALL.ITM~ ~override~ // remove the unwanted effects.
			READ_LONG 0x6A ofsEf
			READ_SHORT 0x70 numEf
			FOR (idx = 0; idx < numEf; ++idx) BEGIN
				READ_SHORT (ofsEf + (0x30 * idx)) opcode
				READ_BYTE (ofsEf + 0x2 + (0x30 * idx)) target
				READ_BYTE (ofsEf + 0x3 + (0x30 * idx)) power
				READ_LONG (ofsEf + 0x4 + (0x30 * idx)) parameter1
				READ_LONG (ofsEf + 0x8 + (0x30 * idx)) parameter2
				READ_BYTE (ofsEf + 0xC + (0x30 * idx)) timing
				READ_BYTE (ofsEf + 0xD + (0x30 * idx)) resist_dispel
				READ_LONG (ofsEf + 0xE + (0x30 * idx)) duration
				READ_BYTE (ofsEf + 0x12 + (0x30 * idx)) probability1
				READ_BYTE (ofsEf + 0x13 + (0x30 * idx)) probability2
				READ_ASCII (ofsEf + 0x14 + (0x30 * idx)) resource
				READ_LONG (ofsEf + 0x1C + (0x30 * idx)) dicenumber
				READ_LONG (ofsEf + 0x20 + (0x30 * idx)) dicesize
				READ_LONG (ofsEf + 0x24 + (0x30 * idx)) savingthrow
				READ_LONG (ofsEf + 0x28 + (0x30 * idx)) savebonus
				READ_LONG (ofsEf + 0x2c + (0x30 * idx)) special
				PATCH_IF NOT ((opcode=7) OR (opcode=98) OR (opcode=126)
				              OR (opcode=135) OR (opcode=144)
				              OR ((opcode=101) AND
				              ((parameter2=210) OR (parameter2=45)))
				              OR((opcode=267) AND
				              ((parameter1=14043) OR (parameter1=1280)))
				              OR ((opcode=142) AND (parameter2=87))) BEGIN
					SPRINT $IronGolEffs(~%opcode%~ ~%target%~ ~%power%~ ~%parameter1%~ ~%parameter2%~ ~%timing%~ ~%resist_dispel%~ ~%duration%~ ~%probability1%~ ~%probability2%~ ~%resource%~ ~%dicenumber%~ ~%dicesize%~ ~%savingthrow%~ ~%savebonus%~ ~%special%~) ~%idx%~
				END
			END
		BUT_ONLY // Iron golem immunities, now in an array.

		COPY_EXISTING ~WOLFGR.ITM~ ~override~ // Greater wolfwere form
			LPF DELETE_EFFECT INT_VAR match_opcode=292 END
			PHP_EACH Imm2Effs AS effs => index BEGIN
				LPF ADD_ITEM_EQEFFECT INT_VAR opcode=effs_0 target=effs_1 power=effs_2 parameter1=effs_3 parameter2=effs_4 timing=effs_5 resist_dispel=effs_6 duration=effs_7 probability1=effs_8 probability2=effs_9 dicenumber=effs_11 dicesize=effs_12 savingthrow=effs_13 savebonus=effs_14 special=effs_15 STR_VAR resource=~%effs_10%~ END
			END
		BUT_ONLY // Replace weapon immunity effect (up to +1) with full array.

		COPY_EXISTING ~EARTHRN.ITM~ ~override~ // Mage earth elemental form
		              ~FIRERN.ITM~ ~override~ // Mage fire elemental form
			      ~cdgoliro.ITM~ ~override~ // Iron golem form
			LPF DELETE_EFFECT INT_VAR match_opcode=120 END
			PHP_EACH Imm3Effs AS effs => index BEGIN
				LPF ADD_ITEM_EQEFFECT INT_VAR opcode=effs_0 target=effs_1 power=effs_2 parameter1=effs_3 parameter2=effs_4 timing=effs_5 resist_dispel=effs_6 duration=effs_7 probability1=effs_8 probability2=effs_9 dicenumber=effs_11 dicesize=effs_12 savingthrow=effs_13 savebonus=effs_14 special=effs_15 STR_VAR resource=~%effs_10%~ END
			END
		BUT_ONLY // Replace weapon immunity effect (up to +2) with full array.

		COPY_EXISTING ~cdgoliro.ITM~ ~override~ // Iron golem form
			PHP_EACH IronGolEffs AS effs => index BEGIN
				LPF ADD_ITEM_EQEFFECT INT_VAR opcode=effs_0 target=effs_1 power=effs_2 parameter1=effs_3 parameter2=effs_4 timing=effs_5 resist_dispel=effs_6 duration=effs_7 probability1=effs_8 probability2=effs_9 dicenumber=effs_11 dicesize=effs_12 savingthrow=effs_13 savebonus=effs_14 special=effs_15 STR_VAR resource=~%effs_10%~ END
			END
		BUT_ONLY // Apply all those missing golem immunities

		COPY_EXISTING ~SPCL611.SPL~ ~override~ // Description changes
		              ~SPWI497.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8010
			SAY DESC @8010
		BUT_ONLY // Brown bear forms
		COPY_EXISTING ~SPCL612.SPL~ ~override~
		              ~SPWI498.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8011
			SAY DESC @8011
		BUT_ONLY // Wolf forms
		COPY_EXISTING ~SPCL613.SPL~ ~override~
		              ~SPWI499.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8012
			SAY DESC @8012
		BUT_ONLY // Black bear forms
		COPY_EXISTING ~SPCL632.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8013
			SAY DESC @8013
		BUT_ONLY // Sword spider form
		COPY_EXISTING ~SPCL633.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8014
			SAY DESC @8014
		BUT_ONLY // Baby wyvern form
		COPY_EXISTING ~SPCL634.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8015
			SAY DESC @8015
		BUT_ONLY // Fire salamander form
		COPY_EXISTING ~SPCL643.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8016
			SAY DESC @8016
		BUT_ONLY // Werewolf form
		COPY_EXISTING ~SPCL644.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8017
			SAY DESC @8017
		BUT_ONLY // Greater werewolf form
		COPY_EXISTING ~SPPR731.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8018
			SAY DESC @8018
		BUT_ONLY // Druid fire elemental form
		COPY_EXISTING ~SPPR732.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8019
			SAY DESC @8019
		BUT_ONLY // Druid earth elemental form
		COPY_EXISTING ~SPWI494.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8021
			SAY DESC @8021
		BUT_ONLY // Ogre form
		COPY_EXISTING ~SPWI495.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8022
			SAY DESC @8022
		BUT_ONLY // Giant spider form
		COPY_EXISTING ~SPIN152.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8024
			SAY DESC @8024
		BUT_ONLY // Mind flayer form
		COPY_EXISTING ~SPIN153.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8025
			SAY DESC @8025
		BUT_ONLY // Iron golem form
		COPY_EXISTING ~SPIN154.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8026
			SAY DESC @8026
		BUT_ONLY // Giant troll form
		COPY_EXISTING ~SPIN155.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8027
			SAY DESC @8027
		BUT_ONLY // Greater Wolfwere form
		COPY_EXISTING ~SPIN156.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8028
			SAY DESC @8028
		BUT_ONLY // Mage fire elemental form
		COPY_EXISTING ~SPIN157.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8029
			SAY DESC @8029
		BUT_ONLY // Mage earth elemental form
	END

BEGIN @8200 DESIGNATED 82 // Druid forms only
SUBCOMPONENT @8000
GROUP @1
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME ~#rdremov.SPL~ @12
	ACTION_IF GAME_IS ~bgee~ THEN BEGIN // BGEE changes
		COPY_EXISTING ~SPCL612.SPL~ ~override~ // Druid wolf form
			LPF ALTER_EFFECT INT_VAR match_opcode=135 STR_VAR match_resource=WOLFCHAR resource=PLYWOLF END
		BUT_ONLY // Use the right polymorph target
		COPY_EXISTING ~WOLFM.ITM~ ~override~
			LPF ALTER_ITEM_HEADER INT_VAR header_type=1 damage_bonus=1 END
		BUT_ONLY // Add +1 damage for consistency with mage wolf form.

		COPY_EXISTING ~brbrp1.ITM~ ~override~ // Greater Werewolf form
			LPF ADD_ITEM_EQEFFECT INT_VAR opcode=98 target=1 parameter1=2 parameter2=3 timing=2 END
			LPF ADD_ITEM_EQEFFECT INT_VAR opcode=142 target=1 parameter1=0 parameter2=87 timing=2 END
		BUT_ONLY // Add regeneration as in BG2EE. Not relevant in standard rules.

		COPY_EXISTING ~SPCL611.SPL~ ~override~ // Description changes
			SAY UNIDENTIFIED_DESC @8010
			SAY DESC @8010
		BUT_ONLY // Brown bear shapeshifts
		COPY_EXISTING ~SPCL612.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8011
			SAY DESC @8011
		BUT_ONLY // Wolf shapeshifts
		COPY_EXISTING ~SPCL613.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8012
			SAY DESC @8012
		BUT_ONLY // Black bear shapeshifts
		COPY_EXISTING ~SPCL632.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8013
			SAY DESC @8013
		BUT_ONLY // Sword spider shapeshift
		COPY_EXISTING ~SPCL633.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8014
			SAY DESC @8014
		BUT_ONLY // Baby wyvern shapeshift
		COPY_EXISTING ~SPCL634.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8015
			SAY DESC @8015
		BUT_ONLY // Fire salamander shapeshift
		COPY_EXISTING ~SPCL643.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8016
			SAY DESC @8016
		BUT_ONLY // Werewolf shapeshift
		COPY_EXISTING ~SPCL644.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8017
			SAY DESC @8017
		BUT_ONLY // Greater werewolf shapeshift
		// No high-level druid forms exist.

		COPY_EXISTING ~PLYBEAR1.ITM~ ~override~
		              ~PLYBEAR2.ITM~ ~override~
		              ~WOLFM.ITM~ ~override~
		              ~PLYSPID2.ITM~ ~override~
		              ~PLYWYVRN.ITM~ ~override~
		              ~PLYSALA.ITM~ ~override~
		              ~BRBRP.ITM~ ~override~
		              ~BRBRP1.ITM~ ~override~
			WRITE_BYTE 0x18 (THIS BOR BIT13)
		BUT_ONLY // Add "forbid off-hand weapon" flag

	END ELSE BEGIN // BGEE and BG2EE polymorph handling differs; split.
		COPY_EXISTING ~PLYBEAR1.ITM~ ~override~ // Bear shapeshifts
			LPF ALTER_EFFECT INT_VAR match_opcode=135 STR_VAR match_resource=BEARBR resource=BEARBL END
		BUT_ONLY
		COPY_EXISTING ~plybear2.ITM~ ~override~
			LPF ALTER_EFFECT INT_VAR match_opcode=135 STR_VAR match_resource=BEARBL resource=BEARBR END
		BUT_ONLY // Swap black and brown bears

		COPY_EXISTING ~WOLFM.ITM~ ~override~ // Druid wolf shapeshift
			LPF ALTER_EFFECT INT_VAR match_opcode=135 STR_VAR match_resource=WOLFCHAR resource=PLYWOLF END
			LPF ALTER_ITEM_HEADER INT_VAR header_type=1 damage_bonus=1 END
		BUT_ONLY // Switch wolf, add +1 damage

		COPY_EXISTING ~SPCL611.SPL~ ~override~ // Description changes
			SAY UNIDENTIFIED_DESC @8010
			SAY DESC @8010
		BUT_ONLY // Brown bear forms
		COPY_EXISTING ~SPCL612.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8011
			SAY DESC @8011
		BUT_ONLY // Wolf forms
		COPY_EXISTING ~SPCL613.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8012
			SAY DESC @8012
		BUT_ONLY // Black bear forms
		COPY_EXISTING ~SPCL632.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8013
			SAY DESC @8013
		BUT_ONLY // Sword spider form
		COPY_EXISTING ~SPCL633.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8014
			SAY DESC @8014
		BUT_ONLY // Baby wyvern form
		COPY_EXISTING ~SPCL634.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8015
			SAY DESC @8015
		BUT_ONLY // Fire salamander form
		COPY_EXISTING ~SPCL643.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8016
			SAY DESC @8016
		BUT_ONLY // Werewolf form
		COPY_EXISTING ~SPCL644.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8017
			SAY DESC @8017
		BUT_ONLY // Greater werewolf form
		COPY_EXISTING ~SPPR731.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8018
			SAY DESC @8018
		BUT_ONLY // Druid fire elemental form
		COPY_EXISTING ~SPPR732.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8019
			SAY DESC @8019
		BUT_ONLY // Druid earth elemental form
	END

BEGIN @8300 DESIGNATED 83 // Mage forms only
SUBCOMPONENT @8000
GROUP @1
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME ~#rdremov.SPL~ @12
	ACTION_IF GAME_IS ~bgee~ THEN BEGIN // BGEE changes
		COPY_EXISTING ~SPWI497.SPL~ ~override~ // Description changes
			SAY UNIDENTIFIED_DESC @8010
			SAY DESC @8010
		BUT_ONLY // Brown bear shapeshifts
		COPY_EXISTING ~SPWI499.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8011
			SAY DESC @8011
		BUT_ONLY // Wolf shapeshifts
		COPY_EXISTING ~SPWI498.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8012
			SAY DESC @8012
		BUT_ONLY // Black bear shapeshifts
		COPY_EXISTING ~SPWI494.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8021
			SAY DESC @8021
		BUT_ONLY // Ogre shapeshift
		COPY_EXISTING ~SPWI495.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8022
			SAY DESC @8022
		BUT_ONLY // Giant spider shapeshift
		// Skip Shapechange forms.

		COPY_EXISTING ~PLYFLIND.ITM~ ~override~
		              ~PLYMSTAR.ITM~ ~override~
		              ~PLYSPID.ITM~ ~override~
		              ~PLYJELLY.ITM~ ~override~
		              ~PLYWOLF1.ITM~ ~override~
		              ~PLYBEAR1.ITM~ ~override~
		              ~PLYBEAR2.ITM~ ~override~
			WRITE_BYTE 0x18 (THIS BOR BIT13)
		BUT_ONLY // Add "forbid off-hand weapon" flag

	END ELSE BEGIN // BGEE and BG2EE polymorph handling differs; split.
		ACTION_CLEAR_ARRAY Imm2Effs
		ACTION_CLEAR_ARRAY Imm3Effs
		ACTION_CLEAR_ARRAY IronGolEffs

		COPY_EXISTING ~PLYBEAR1.ITM~ ~override~ // Bear shapeshifts
			LPF ALTER_EFFECT INT_VAR match_opcode=135 STR_VAR match_resource=BEARBR resource=BEARBL END
		BUT_ONLY
		COPY_EXISTING ~plybear2.ITM~ ~override~
			LPF ALTER_EFFECT INT_VAR match_opcode=135 STR_VAR match_resource=BEARBL resource=BEARBR END
		BUT_ONLY // Swap black and brown bears

		COPY_EXISTING ~PLYMSTAR.ITM~ ~override~ // Ogre shapeshift
			READ_LONG 0x18 flags
			WRITE_LONG 0x18 (flags BOR BIT6)
		BUT_ONLY // Weapon becomes magical

		COPY_EXISTING ~SPWI498.SPL~ ~override~ // Mage black bear shapeshift
			LPF ALTER_EFFECT INT_VAR match_opcode=111 STR_VAR match_resource=PLYBEAR2 resource=PLYBEAR1 END
		BUT_ONLY // Use a different bear from SPWI497 brown bear shapeshift

		COPY_EXISTING ~IMMUNE2.ITM~ ~override~ // Immunity to weapons up to +1
			READ_LONG 0x6A ofsEf
			READ_SHORT 0x70 numEf
			FOR (idx = 0; idx < numEf; ++idx) BEGIN
				READ_SHORT (ofsEf + (0x30 * idx)) opcode
				READ_BYTE (ofsEf + 0x2 + (0x30 * idx)) target
				READ_BYTE (ofsEf + 0x3 + (0x30 * idx)) power
				READ_LONG (ofsEf + 0x4 + (0x30 * idx)) parameter1
				READ_LONG (ofsEf + 0x8 + (0x30 * idx)) parameter2
				READ_BYTE (ofsEf + 0xC + (0x30 * idx)) timing
				READ_BYTE (ofsEf + 0xD + (0x30 * idx)) resist_dispel
				READ_LONG (ofsEf + 0xE + (0x30 * idx)) duration
				READ_BYTE (ofsEf + 0x12 + (0x30 * idx)) probability1
				READ_BYTE (ofsEf + 0x13 + (0x30 * idx)) probability2
				READ_ASCII (ofsEf + 0x14 + (0x30 * idx)) resource
				READ_LONG (ofsEf + 0x1C + (0x30 * idx)) dicenumber
				READ_LONG (ofsEf + 0x20 + (0x30 * idx)) dicesize
				READ_LONG (ofsEf + 0x24 + (0x30 * idx)) savingthrow
				READ_LONG (ofsEf + 0x28 + (0x30 * idx)) savebonus
				READ_LONG (ofsEf + 0x2c + (0x30 * idx)) special
				SPRINT $Imm2Effs(~%opcode%~ ~%target%~ ~%power%~ ~%parameter1%~ ~%parameter2%~ ~%timing%~ ~%resist_dispel%~ ~%duration%~ ~%probability1%~ ~%probability2%~ ~%resource%~ ~%dicenumber%~ ~%dicesize%~ ~%savingthrow%~ ~%savebonus%~ ~%special%~) ~%idx%~
			END
		BUT_ONLY // Copy all effects to an array for later use. Original unchanged.

		COPY_EXISTING ~IMMUNE3.ITM~ ~override~ // Immunity to weapons up to +2
			READ_LONG 0x6A ofsEf
			READ_SHORT 0x70 numEf
			FOR (idx = 0; idx < numEf; ++idx) BEGIN
				READ_SHORT (ofsEf + (0x30 * idx)) opcode
				READ_BYTE (ofsEf + 0x2 + (0x30 * idx)) target
				READ_BYTE (ofsEf + 0x3 + (0x30 * idx)) power
				READ_LONG (ofsEf + 0x4 + (0x30 * idx)) parameter1
				READ_LONG (ofsEf + 0x8 + (0x30 * idx)) parameter2
				READ_BYTE (ofsEf + 0xC + (0x30 * idx)) timing
				READ_BYTE (ofsEf + 0xD + (0x30 * idx)) resist_dispel
				READ_LONG (ofsEf + 0xE + (0x30 * idx)) duration
				READ_BYTE (ofsEf + 0x12 + (0x30 * idx)) probability1
				READ_BYTE (ofsEf + 0x13 + (0x30 * idx)) probability2
				READ_ASCII (ofsEf + 0x14 + (0x30 * idx)) resource
				READ_LONG (ofsEf + 0x1C + (0x30 * idx)) dicenumber
				READ_LONG (ofsEf + 0x20 + (0x30 * idx)) dicesize
				READ_LONG (ofsEf + 0x24 + (0x30 * idx)) savingthrow
				READ_LONG (ofsEf + 0x28 + (0x30 * idx)) savebonus
				READ_LONG (ofsEf + 0x2c + (0x30 * idx)) special
				SPRINT $Imm3Effs(~%opcode%~ ~%target%~ ~%power%~ ~%parameter1%~ ~%parameter2%~ ~%timing%~ ~%resist_dispel%~ ~%duration%~ ~%probability1%~ ~%probability2%~ ~%resource%~ ~%dicenumber%~ ~%dicesize%~ ~%savingthrow%~ ~%savebonus%~ ~%special%~) ~%idx%~
			END
		BUT_ONLY // Copy all effects to an array for later use. Original unchanged.

		COPY_EXISTING ~AMUL22.ITM~ ~override~ // Iron golem immunity effects
		              ~CLCK30.ITM~ ~override~ // This is a serious hack job.
		              ~HELM06.ITM~ ~override~ // Combine four items and
		              ~TROLLALL.ITM~ ~override~ // remove the unwanted effects.
			READ_LONG 0x6A ofsEf
			READ_SHORT 0x70 numEf
			FOR (idx = 0; idx < numEf; ++idx) BEGIN
				READ_SHORT (ofsEf + (0x30 * idx)) opcode
				READ_BYTE (ofsEf + 0x2 + (0x30 * idx)) target
				READ_BYTE (ofsEf + 0x3 + (0x30 * idx)) power
				READ_LONG (ofsEf + 0x4 + (0x30 * idx)) parameter1
				READ_LONG (ofsEf + 0x8 + (0x30 * idx)) parameter2
				READ_BYTE (ofsEf + 0xC + (0x30 * idx)) timing
				READ_BYTE (ofsEf + 0xD + (0x30 * idx)) resist_dispel
				READ_LONG (ofsEf + 0xE + (0x30 * idx)) duration
				READ_BYTE (ofsEf + 0x12 + (0x30 * idx)) probability1
				READ_BYTE (ofsEf + 0x13 + (0x30 * idx)) probability2
				READ_ASCII (ofsEf + 0x14 + (0x30 * idx)) resource
				READ_LONG (ofsEf + 0x1C + (0x30 * idx)) dicenumber
				READ_LONG (ofsEf + 0x20 + (0x30 * idx)) dicesize
				READ_LONG (ofsEf + 0x24 + (0x30 * idx)) savingthrow
				READ_LONG (ofsEf + 0x28 + (0x30 * idx)) savebonus
				READ_LONG (ofsEf + 0x2c + (0x30 * idx)) special
				PATCH_IF NOT ((opcode=7) OR (opcode=98) OR (opcode=126) OR
				              (opcode=135) OR (opcode=144) OR
				              ((opcode=101) AND
				              ((parameter2=210) OR (parameter2=45))) OR
				              ((opcode=267) AND
				              ((parameter1=14043) OR (parameter1=1280)))
				              OR ((opcode=142) AND (parameter2=87))) BEGIN
					SPRINT $IronGolEffs(~%opcode%~ ~%target%~ ~%power%~ ~%parameter1%~ ~%parameter2%~ ~%timing%~ ~%resist_dispel%~ ~%duration%~ ~%probability1%~ ~%probability2%~ ~%resource%~ ~%dicenumber%~ ~%dicesize%~ ~%savingthrow%~ ~%savebonus%~ ~%special%~) ~%idx%~
				END
			END
		BUT_ONLY // Iron golem immunities, now in an array.

		COPY_EXISTING ~WOLFGR.ITM~ ~override~ // Greater wolfwere form
			LPF DELETE_EFFECT INT_VAR match_opcode=292 END
			PHP_EACH Imm2Effs AS effs => index BEGIN
				LPF ADD_ITEM_EQEFFECT INT_VAR opcode=effs_0 target=effs_1 power=effs_2 parameter1=effs_3 parameter2=effs_4 timing=effs_5 resist_dispel=effs_6 duration=effs_7 probability1=effs_8 probability2=effs_9 dicenumber=effs_11 dicesize=effs_12 savingthrow=effs_13 savebonus=effs_14 special=effs_15 STR_VAR resource=~%effs_10%~ END
			END
		BUT_ONLY // Replace weapon immunity effect (up to +1) with full array.

		COPY_EXISTING ~EARTHRN.ITM~ ~override~ // Mage earth elemental form
		              ~FIRERN.ITM~ ~override~ // Mage fire elemental form
			      ~cdgoliro.ITM~ ~override~ // Iron golem form
			LPF DELETE_EFFECT INT_VAR match_opcode=120 END
			PHP_EACH Imm3Effs AS effs => index BEGIN
				LPF ADD_ITEM_EQEFFECT INT_VAR opcode=effs_0 target=effs_1 power=effs_2 parameter1=effs_3 parameter2=effs_4 timing=effs_5 resist_dispel=effs_6 duration=effs_7 probability1=effs_8 probability2=effs_9 dicenumber=effs_11 dicesize=effs_12 savingthrow=effs_13 savebonus=effs_14 special=effs_15 STR_VAR resource=~%effs_10%~ END
			END
		BUT_ONLY // Replace weapon immunity effect (up to +2) with full array.

		COPY_EXISTING ~cdgoliro.ITM~ ~override~ // Iron golem form
			PHP_EACH IronGolEffs AS effs => index BEGIN
				LPF ADD_ITEM_EQEFFECT INT_VAR opcode=effs_0 target=effs_1 power=effs_2 parameter1=effs_3 parameter2=effs_4 timing=effs_5 resist_dispel=effs_6 duration=effs_7 probability1=effs_8 probability2=effs_9 dicenumber=effs_11 dicesize=effs_12 savingthrow=effs_13 savebonus=effs_14 special=effs_15 STR_VAR resource=~%effs_10%~ END
			END
		BUT_ONLY // Apply all those missing golem immunities

		COPY_EXISTING ~SPWI497.SPL~ ~override~ // Description changes
			SAY UNIDENTIFIED_DESC @8010
			SAY DESC @8010
		BUT_ONLY // Brown bear forms
		COPY_EXISTING ~SPWI498.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8011
			SAY DESC @8011
		BUT_ONLY // Wolf forms
		COPY_EXISTING ~SPWI499.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8012
			SAY DESC @8012
		BUT_ONLY // Black bear forms
		COPY_EXISTING ~SPWI494.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8021
			SAY DESC @8021
		BUT_ONLY // Ogre form
		COPY_EXISTING ~SPWI495.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8022
			SAY DESC @8022
		BUT_ONLY // Giant spider form
		COPY_EXISTING ~SPIN152.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8024
			SAY DESC @8024
		BUT_ONLY // Mind flayer form
		COPY_EXISTING ~SPIN153.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8025
			SAY DESC @8025
		BUT_ONLY // Iron golem form
		COPY_EXISTING ~SPIN154.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8026
			SAY DESC @8026
		BUT_ONLY // Giant troll form
		COPY_EXISTING ~SPIN155.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8027
			SAY DESC @8027
		BUT_ONLY // Greater Wolfwere form
		COPY_EXISTING ~SPIN156.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8028
			SAY DESC @8028
		BUT_ONLY // Mage fire elemental form
		COPY_EXISTING ~SPIN157.SPL~ ~override~
			SAY UNIDENTIFIED_DESC @8029
			SAY DESC @8029
		BUT_ONLY // Mage earth elemental form
	END

//
//
// Wish Hardiness doesn't stack with itself
//
//

BEGIN @9000 DESIGNATED 90
GROUP @1
	REQUIRE_PREDICATE GAME_IS ~bg2ee eet~ @11
	COPY_EXISTING ~SPWISH12.SPL~ ~override~
		LPF ALTER_EFFECT INT_VAR match_opcode=321 target=3 END
	BUT_ONLY // Now removes hardiness effects from the same targets it adds them to

//
//
// Hold Person doesn't display multiple strings
//
//

BEGIN @10000 DESIGNATED 100
GROUP @1
	COPY_EXISTING_REGEXP GLOB ~^.+\.SPL$~ ~override~
		LPF DELETE_EFFECT INT_VAR match_opcode=177 STR_VAR match_resource=CDHELD END
	BUT_ONLY // Engine applies "Held" string; explicit effect not needed

//
//
// Barbarian Rage blocks cosmetic effects
//
//

BEGIN @11000 DESIGNATED 110
GROUP @1
	REQUIRE_PREDICATE GAME_IS ~bgee~ @14
	COPY_EXISTING ~SPCL152.SPL~ ~override~ // Barbarian rage
		LPF CLONE_EFFECT INT_VAR match_opcode=321 STR_VAR insert=first match_resource=SPCL152 resource=SPCL102 END
		LPF DELETE_EFFECT INT_VAR match_opcode=321 STR_VAR match_resource=SPCL152  END
		LPF ALTER_EFFECT INT_VAR match_opcode=321 STR_VAR match_resource=SPCL102 resource=SPCL152 END
	BUT_ONLY // Effect removing previous instances of rage needs to be first in stack

//
//
// Dueling fireshields don't go infinite
//
//

BEGIN @12000 DESIGNATED 120
GROUP @1
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME ~#rdremov.SPL~ @12
	ACTION_CLEAR_ARRAY fireshields
	OUTER_SET fsid = 0
	OUTER_SET OTR5 = IDS_OF_SYMBOL (~MISSILE~ ~OneTarget_Range_05~)
	OUTER_SET OTR7 = IDS_OF_SYMBOL (~MISSILE~ ~OneTarget_Range_07~)
	OUTER_SET OTR10 = IDS_OF_SYMBOL (~MISSILE~ ~OneTarget_Range_10~)
	// Needed due to BGEE/BG2EE differences. Old code used BG2EE values.
	ACTION_IF (OTR5 < 0) BEGIN
		OUTER_SET OTR5 = IDS_OF_SYMBOL (~PROJECTL~ ~RANGE05~) + 1
	END
	ACTION_IF (OTR7 < 0) BEGIN
		OUTER_SET OTR7 = IDS_OF_SYMBOL (~PROJECTL~ ~RANGE07~) + 1
	END
	ACTION_IF (OTR10 < 0) BEGIN
		OUTER_SET OTR10 = IDS_OF_SYMBOL (~PROJECTL~ ~RANGE10~) + 1
	END
	// Backup plan if MISSILE.IDS can't be read. Not needed with WeiDU 248 or newer.

	COPY_EXISTING_REGEXP GLOB ~^.+\.SPL$~ ~override~
		READ_LONG 0x64 ofsAb
		READ_SHORT 0x68 numAb
		FOR (idx = 0; idx < numAb; ++idx) BEGIN
			SET ofsProj = ofsAb + 0x26 + (idx * 40)
			READ_SHORT ofsProj proj
			PATCH_IF ((proj = OTR5) OR (proj = OTR7) OR (proj = OTR10)) BEGIN
				SPRINT $fireshields(~%fsid%~) ~%SOURCE_RES%~
				SET fsid = fsid + 1
				SET idx = numAb
			END
		END
	BUT_ONLY // First pass: find the fireshields, make an array.

	ACTION_IF (debug_messages) BEGIN
		PRINT ~%fsid% fireshields found. Patching spells.~ // Debug/status message
	END

	ACTION_PHP_EACH fireshields AS index1 => spell BEGIN
		COPY_EXISTING ~%spell%.SPL~ ~override~
			READ_LONG 0x64 ofsAb
			READ_SHORT 0x68 numAb
			FOR (idx = 0; idx < numAb; ++idx) BEGIN
				SET ofsProj = ofsAb + 0x26 + (idx * 40)
				READ_SHORT ofsProj proj
				PATCH_IF ((proj = OTR5) OR (proj = OTR7) OR (proj = OTR10)) BEGIN
					PHP_EACH fireshields AS index2 => fsref BEGIN
						LPF ADD_SPELL_EFFECT INT_VAR opcode=206 target=1 timing=10 duration=1 header=idx+1 STR_VAR resource=~%fsref%~ END
					END
				END
			END
		BUT_ONLY // Each hit grants 1-tick immunity to all fireshields
	END // Since we already know which spells are fireshields, copy only those.

//
//
// Standardize Wand of Missiles
//
//

BEGIN @13000 DESIGNATED 130
GROUP @1
REQUIRE_PREDICATE GAME_IS ~bgee~ @14
/*	INCLUDE ~jtweaks/resource/functions.tph~ (New function ALTER_CRE_ITEM needed) */
	COPY_EXISTING 	~IMOEN1.CRE~ override
			~IMOEN2.CRE~ override
			~IMOEN4.CRE~ override
			~IMOEN6.CRE~ override
			~MORDAI.CRE~ override
		LPF ALTER_CRE_ITEM STR_VAR match_resref=WAND12 resref=WAND03 END
	BUT_ONLY // Nonstandard wand of missiles on Imoen and Mordaine replaced

//
//
// Inquisitor True Sight is consistent with other versions of the spell
//
//

BEGIN @14000 DESIGNATED 140
GROUP @1
	COPY_EXISTING ~SPCL232.SPL~ ~override~
		LPF ALTER_SPELL_EFFECT INT_VAR power = 0 END
	BUT_ONLY // All effects hit as level zero, like subspell and other TS versions

//
//
// Temple cures work better
//
//

BEGIN @15000 DESIGNATED 150
GROUP @2
/* INCLUDE ~jtweaks/resource/functions.tph~ (New function ALTER_STORE_CURE needed) */
	COPY_EXISTING ~SPPR303.spl~ ~override/J8#TMDIS.SPL~
		LPF ALTER_SPELL_HEADER INT_VAR header_type=1 target=1 projectile=1 END
		LPF ALTER_SPELL_EFFECT INT_VAR match_opcode=58 parameter1=0 END
	BUT_ONLY // Single-target dispel, no level check
	COPY_EXISTING ~SPPR417.spl~ ~override/J8#LRNF.SPL~
		LPF DELETE_EFFECT INT_VAR match_opcode=93 END
	BUT_ONLY // Lesser restoration without fatigue
	COPY_EXISTING ~SPPR713.spl~ ~override/J8#GRNF.SPL~
		LPF DELETE_EFFECT INT_VAR match_opcode=93 END
	BUT_ONLY // Greater restoration without fatigue
	COPY_EXISTING_REGEXP GLOB ~^.+\.STO$~ ~override~
		LPF ALTER_STORE_CURE STR_VAR match_resref=SPPR303 resref=J8#TMDIS END
		LPF ALTER_STORE_CURE STR_VAR match_resref=SPPR417 resref=J8#LRNF END
		LPF ALTER_STORE_CURE STR_VAR match_resref=SPPR713 resref=J8#GRNF END
	BUT_ONLY // Replace Dispel Magic, Lesser Restoration, Greater Restoration.

//
//
// Bala's Axe dispels reasonably well
//
//

BEGIN @16000 DESIGNATED 160
GROUP @2
	COPY_EXISTING ~SPPR303.SPL~ ~override/J8#indis.SPL~
		WRITE_SHORT 0x1c 4 // Clone Dispel Magic, make it innate
	COPY_EXISTING ~AX1H07.itm~ ~override~ // Bala's Axe: Wizard Slayer
		LPF ALTER_ITEM_EFFECT INT_VAR check_headers=1 match_opcode=148 STR_VAR resource=J8#indis END
	BUT_ONLY

//
//
// Celestials cast all of their spells at high levels
//
//

BEGIN @17000 DESIGNATED 170
GROUP @2
REQUIRE_PREDICATE GAME_IS ~bg2ee eet~ @11
	OUTER_SET solar = IDS_OF_SYMBOL (~RACE~ ~SOLAR~)
	OUTER_SET dplan = IDS_OF_SYMBOL (~RACE~ ~DARKPLANATAR~)
	// IDS check probably not strictly needed; RACE.IDS is the same between games.
	COPY_EXISTING_REGEXP GLOB ~^.+\.CRE$~ ~override~
		READ_BYTE 0x272 race
		PATCH_IF ((race >= solar) AND (race <= dplan)) BEGIN
			READ_BYTE 0x234 level1
			READ_BYTE 0x235 level2
			READ_BYTE 0x273 class
			PATCH_IF ((level1=25 AND level2=1) AND (class=1 OR class=3)) BEGIN
				WRITE_BYTE 0x273 14
				WRITE_BYTE 0x235 25
			END
		END
	BUT_ONLY // Changing pure clerics and pure mages to cleric/mages.

	COPY_EXISTING ~DEVA.ITM~ ~override~
	              ~PLANETAR.ITM~ ~override~
		LPF CLONE_EFFECT INT_VAR match_opcode=58 parameter1=25 match_parameter2=131073 parameter2=131074 STR_VAR insert=last END
		LPF DELETE_EFFECT INT_VAR multi_match=1 match_opcode=58 match_parameter2=131073 END
	BUT_ONLY // Dispel at level 25, moved to end.

//
//
// Antimagic rays and Spell Shield
//
//

BEGIN @18100 DESIGNATED 181
SUBCOMPONENT @18000
GROUP @2
	ACTION_FOR_EACH spell IN ~SPIN550.SPL~ ~SPIN992.SPL~ BEGIN
		ACTION_IF FILE_EXISTS_IN_GAME ~%spell%~ THEN BEGIN
			COPY_EXISTING ~%spell%~ ~override~
				WRITE_BYTE 0x25 0 // School becomes "None"
				WRITE_BYTE 0x27 0 // Secondary type becomes "None"
			BUT_ONLY
		END
	END // Beholder ray exists in BGEE (Black Pits), but not Hive Mother ray.

BEGIN @18200 DESIGNATED 182
SUBCOMPONENT @18000
GROUP @2
	ACTION_FOR_EACH spell IN ~SPIN550.SPL~ ~SPIN992.SPL~ BEGIN
		ACTION_IF FILE_EXISTS_IN_GAME ~%spell%~ THEN BEGIN
			COPY_EXISTING ~%spell%~ ~override~
				LPF ALTER_SPELL_EFFECT INT_VAR power = 10 END
			BUT_ONLY
		END
	END // Same wrapper, different action. Level 10 beats all other protections.

//
//
// Death Ward protects against Aec'Letec's Death Gaze
//
//

BEGIN @19000 DESIGNATED 190
GROUP @2
REQUIRE_PREDICATE GAME_IS ~bgee eet~ @13
	ACTION_IF GAME_IS bgee BEGIN
		OUTER_SPRINT death_gaze SPIN996
	END ELSE BEGIN
		OUTER_SPRINT death_gaze BGIN996
	END // Spell codes for EET compatibility
	COPY_EXISTING_REGEXP ".*\.ITM" ~override~ ".*\.SPL" ~override~
		LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=169 match_parameter2=51 opcode=206 parameter2=0 STR_VAR resource=~%death_gaze%~ END
	BUT_ONLY // Copy immunity to "dying" icon and convert to specific immunity.

//
//
// Elemental protection spells increment resistances
//
//

BEGIN @20000 DESIGNATED 200
GROUP @2
	ACTION_FOR_EACH spell IN ~SPCL721.SPL~ ~SPPR210.SPL~ ~SPPR306.SPL~ ~SPPR407.SPL~ ~SPPR730.SPL~ ~SPWI319.SPL~ ~SPWI320.SPL~ ~SPWI403.SPL~ ~SPWI418.SPL~ ~SPWI512.SPL~ ~SPWI517.SPL~ ~SPWI606.SPL~ ~SPWI702.SPL~ ~SPWI803.SPL~
		BEGIN // Resistance spells
		ACTION_IF FILE_EXISTS_IN_GAME ~%spell%~ THEN BEGIN
			COPY_EXISTING ~%spell%~ ~override~
				LPF ALTER_SPELL_EFFECT INT_VAR match_opcode=27 parameter2=0 END
				LPF ALTER_SPELL_EFFECT INT_VAR match_opcode=28 parameter2=0 END
				LPF ALTER_SPELL_EFFECT INT_VAR match_opcode=29 parameter2=0 END
				LPF ALTER_SPELL_EFFECT INT_VAR match_opcode=30 parameter2=0 END
				LPF ALTER_SPELL_EFFECT INT_VAR match_opcode=31 parameter2=0 END
			BUT_ONLY END 
		END // Change elemental resistance effects to "increment" mode.

//
//
// Vorpal weapons don't chunk
//
//

BEGIN @21000 DESIGNATED 210
GROUP @2
	COPY_EXISTING_REGEXP GLOB ~^.+\.ITM$~ ~override~
		LPF ALTER_EFFECT INT_VAR silent=1 check_globals=0 check_headers=1 header_type=1 match_opcode=13 match_timing=1 parameter2=4 END
		LPF ALTER_EFFECT INT_VAR silent=1 check_globals=0 check_headers=1 header_type=2 match_opcode=13 match_timing=1 parameter2=4 END
	BUT_ONLY

//
//
// Resurrection can heal the living
//
//

BEGIN @22000 DESIGNATED 220
GROUP @2
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME ~#rdremov.SPL~ @12
	ACTION_IF ((FILE_EXISTS_IN_GAME ~BHAAL4A.SPL~) AND NOT (FILE_EXISTS_IN_GAME ~BHAAL4AH.SPL~)) THEN BEGIN
		COPY_EXISTING ~BHAAL4AA.SPL~ ~override/BHAAL4AH.SPL~
			LPF DELETE_EFFECT INT_VAR match_opcode=32 END
			LPF DELETE_EFFECT INT_VAR match_opcode=326 END
		BUT_ONLY // Create healing-only subspell
		COPY_EXISTING ~BHAAL4A.SPL~ ~override~
			LPF CLONE_EFFECT INT_VAR match_opcode=326 parameter2=113 STR_VAR match_resource=BHAAL4AA resource=BHAAL4AH insert=first END
			LPF DELETE_EFFECT INT_VAR match_opcode=324 END
		BUT_ONLY // Cast new subspell on living targets. Remove immunity message.
	END // ToB Bhaal power resurrection. Not used in vanilla game.
	ACTION_IF ((FILE_EXISTS_IN_GAME ~SPPR712.SPL~) AND NOT (FILE_EXISTS_IN_GAME ~SPPR712H.SPL~)) THEN BEGIN
		COPY_EXISTING ~SPPR712A.SPL~ ~override/SPPR712H.spl~
			LPF DELETE_EFFECT INT_VAR match_opcode=32 END
			LPF DELETE_EFFECT INT_VAR match_opcode=326 END
		BUT_ONLY // Create healing-only subspell
		COPY_EXISTING ~SPPR712.SPL~ ~override~ // Resurrection
			LPF CLONE_EFFECT INT_VAR match_opcode=326 parameter2=113 STR_VAR match_resource=SPPR712A resource=SPPR712H insert=first END
			LPF DELETE_EFFECT INT_VAR match_opcode=324 END
		BUT_ONLY // Cast new subspell on living targets. Remove immunity message.
		ACTION_IF FILE_EXISTS_IN_GAME ~RODS03.ITM~ THEN BEGIN
			COPY_EXISTING ~RODS03.ITM~ ~override~ // Rod of Resurrection
				LPF CLONE_EFFECT INT_VAR match_opcode=326 parameter2=113 STR_VAR match_resource=SPPR712A resource=SPPR712H insert=first END
				LPF DELETE_EFFECT INT_VAR match_opcode=324 END
			BUT_ONLY // Cast subspell on living targets. No immunity message.
		END
	END
	ACTION_IF ((FILE_EXISTS_IN_GAME ~SPPR729.SPL~) AND NOT (FILE_EXISTS_IN_GAME ~SPPR729H.SPL~)) THEN BEGIN
		COPY_EXISTING ~SPPR729A.SPL~ ~override/SPPR729H.SPL~
			LPF DELETE_EFFECT INT_VAR match_opcode=32 END
			LPF DELETE_EFFECT INT_VAR match_opcode=326 END
		BUT_ONLY // Create healing-only subspell
		COPY_EXISTING ~SPPR729.SPL~ ~override~ // Mass Raise Dead
			LPF CLONE_EFFECT INT_VAR match_opcode=326 parameter2=113 STR_VAR match_resource=SPPR729A resource=SPPR729H insert=first END
		BUT_ONLY // Cast new subspell on living targets
	END

//
//
// Loosen restrictions on what clones can do
//
//

BEGIN @23000 DESIGNATED 230
GROUP @2
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME ~#rdremov.SPL~ @12
	COPY_EXISTING ~PROJIMAG.SPL~ ~override~
	              ~SIMULACR.SPL~ ~override~
		LPF ALTER_SPELL_EFFECT INT_VAR match_opcode=144 match_parameter2 = 14 parameter2=1 END
		LPF DELETE_EFFECT INT_VAR match_opcode=144 match_parameter2 = 0 END
		LPF DELETE_EFFECT INT_VAR match_opcode=206 STR_VAR match_resource=SPWI420 END
		LPF DELETE_EFFECT INT_VAR match_opcode=206 STR_VAR match_resource=SPWI617 END
		LPF DELETE_EFFECT INT_VAR match_opcode=206 STR_VAR match_resource=SPWI710 END
		LPF DELETE_EFFECT INT_VAR match_opcode=206 STR_VAR match_resource=SPWI809 END
		LPF DELETE_EFFECT INT_VAR match_opcode=206 STR_VAR match_resource=SPWI908 END
	BUT_ONLY // Can't use "thieving". Can detect traps, hide, and use sequencers/contingencies.

//
//
// Undead Hunters are better at Turn Undead
//
//

BEGIN @24000 DESIGNATED 240
GROUP @2
	COPY_EXISTING ~SPCL241.SPL~ ~override/J8#TURN1.SPL~
		LPF DELETE_EFFECT END
		LPF ADD_SPELL_EFFECT INT_VAR opcode=323 target=2 timing=9 parameter1=1 END
	BUT_ONLY // New spell adds 1 to turning level
	COPY_EXISTING ~SPCL241.SPL~ ~override/J8#TURN2.SPL~
		LPF DELETE_EFFECT END
		LPF ADD_SPELL_EFFECT INT_VAR opcode=323 target=2 timing=9 parameter1=2 END
	BUT_ONLY // New spell adds 2 to turning level

	COPY_EXISTING  ~CLABPA04.2DA~ ~override~
		COUNT_2DA_COLS colcount
		COUNT_2DA_ROWS colcount rowcount
		SET newcount = rowcount+1
		SPRINT newrow ~ABILITY~
		SPRINT newrow ~%newrow%~^~%newcount%~
		SPRINT entry ~~
		FOR (col=1; col < colcount; ++col) BEGIN
			PATCH_IF (col=3) BEGIN
				SPRINT entry ~AP_J8#TURN2~
			END ELSE PATCH_IF (((col>9) AND (col<30)) AND (col MODULO 3 =1)) BEGIN
				SPRINT entry ~AP_J8#TURN1~
			END ELSE BEGIN
				SPRINT entry ~****       ~
			END
			SPRINT newrow ~%newrow%~^~%TAB%~^~%entry%~
		END
		INSERT_2DA_ROW rowcount colcount ~%newrow%~
	BUT_ONLY // Add row to Undead Hunter CLAB to apply new spells

	OUTER_SET UHDesc = RESOLVE_STR_REF (@24010) // New kit description

	COPY_EXISTING ~KITLIST.2DA~ ~override~
		READ_2DA_ENTRIES_NOW rows 10
		FOR (row=0; row < rows; ++row) BEGIN
			READ_2DA_ENTRY_FORMER rows row 1 name
			PATCH_IF (~%name%~ STR_EQ ~UNDEAD_HUNTER~) BEGIN
				SET UH_row = %row%
			END
		END
		SET_2DA_ENTRY %UH_row% 4 10 %UHDesc%
	BUT_ONLY
	COPY_EXISTING ~clastext.2DA~ ~override~
		READ_2DA_ENTRIES_NOW rows 9
		FOR (row=0; row < rows; ++row) BEGIN
			READ_2DA_ENTRY_FORMER rows row 0 name
			PATCH_IF (~%name%~ STR_EQ ~UNDEAD_HUNTER~) BEGIN
				SET UH_row = %row%
			END
			PATCH_IF (~%name%~ STR_EQ ~FALLEN_UNDEAD_HUNTER~) BEGIN
				SET UHF_row = %row%
			END
		END
		SET_2DA_ENTRY %UH_row% 4 9 %UHDesc%
		SET_2DA_ENTRY %UHF_row% 4 9 %UHDesc%
	BUT_ONLY
	COPY_EXISTING ~ENGINEST.2DA~ ~override~
		READ_2DA_ENTRIES_NOW rows 2
		FOR (row=0; row < rows; ++row) BEGIN
			READ_2DA_ENTRY_FORMER rows row 0 label
			PATCH_IF (~%label%~ STR_EQ ~STRREF_GUI_HELP_KIT_UNDEADHUNTER~) BEGIN
				SET UH_row = %row%
			END
		END
		SET_2DA_ENTRY %UH_row% 1 2 %UHDesc%
	BUT_ONLY // Reference new description in the four locations it's needed
	ACTION_IF (FILE_EXISTS_IN_GAME ~sodcltxt.2da~) BEGIN
		COPY_EXISTING ~sodcltxt.2da~ ~override~
			COUNT_2DA_COLS cols // I don't know this one in advance
			READ_2DA_ENTRIES_NOW rows cols
			FOR (row=0; row < rows; ++row) BEGIN
				READ_2DA_ENTRY_FORMER rows row 0 name
				PATCH_IF ~%name%~ STR_EQ ~UNDEAD_HUNTER~ BEGIN
					SET UH_row = %row%
				END
				PATCH_IF ~%name%~ STR_EQ ~FALLEN_UNDEAD_HUNTER~ BEGIN
					SET UHF_row = %row%
				END
			END
			SET_2DA_ENTRY %UH_row% 4 cols %UHDesc%
			SET_2DA_ENTRY %UHF_row% 4 cols %UHDesc%
		BUT_ONLY
	END // And apparently two more for SoD

//
//
// Loosen NPC item restrictions
//
//

BEGIN @25000 DESIGNATED 250
GROUP @2
	COPY_EXISTING ~AROW14.ITM~ ~override~ // Eldoth's arrows
		WRITE_LONG 0x1e 0x2004D780
		WRITE_BYTE 0x29 0
	BUT_ONLY // Same usability as standard arrows. Present in both games.

	ACTION_IF GAME_IS ~bg2ee eet~ THEN BEGIN
		COPY_EXISTING ~item_use.2da~ ~override~
			READ_2DA_ENTRIES_NOW rows 5
			SET doubled = 0
			SET duperow = 0
			FOR (row=0; row < rows; ++row) BEGIN
				READ_2DA_ENTRY_FORMER rows row 0 item
				PATCH_IF (~%item%~ STR_EQ ~NPSW03~) BEGIN
					SET doubled +=1
					PATCH_IF (doubled = 2) BEGIN
						SET duperow = %row%
						SET doubled = 99
					END
				END
			END
			PATCH_IF (duperow > 0) BEGIN
				SET_2DA_ENTRY duperow 0 5 ~NPSTAF~
				SET_2DA_ENTRY duperow 1 5 ~CERND~
				SET_2DA_ENTRY duperow 4 5 9153
			END
			UNLESS ~NPSTAF~ // Skip if missed item is already there
		BUT_ONLY // Remove duplicate entry, replace with missed item

		COPY_EXISTING ~NPARM.ITM~ ~override~ // Jan's armor
			WRITE_LONG 0x1e 0x20040000
			WRITE_BYTE 0x29 0x10
		BUT_ONLY // Leather armor usability, dex requirement retained

		COPY_EXISTING ~NPBOW.ITM~ ~override~ // Mazzy's bow
			WRITE_LONG 0x1e 0xBB84D780
			WRITE_BYTE 0x2f 0x0C
			LPF ADD_ITEM_EQEFFECT INT_VAR opcode=319 target=1 timing=2 parameter1=11 parameter2=5 special=1080 END
		BUT_ONLY // Shortbow usability plus halfling requirement

		COPY_EXISTING ~NPCHAN.ITM~ ~override~ // Valygar's armor
			WRITE_LONG 0x1e 0x60040000
			WRITE_BYTE 0x29 0x30
			WRITE_BYTE 0x2b 0
			WRITE_BYTE 0x2d 0
			WRITE_BYTE 0x2f 0x04
		BUT_ONLY // Elven chain usability plus ranger kits allowed, attribute reqs retained

		COPY_EXISTING ~NPCLCK.ITM~ ~override~ // Cernd's cloak
			WRITE_LONG 0x1e 0
			WRITE_SHORT 0x26 0
			WRITE_BYTE 0x29 0
			WRITE_BYTE 0x2a 0
			WRITE_BYTE 0x2c 0
			WRITE_BYTE 0x2e 0
			WRITE_BYTE 0x2f 0x02
			WRITE_BYTE 0x30 0
			WRITE_BYTE 0x32 0
		BUT_ONLY // No class, alignment, or attribute restrictions. Except wizard slayers.

		COPY_EXISTING ~NPMISC1.ITM~ ~override~ // Jan's hat
			WRITE_LONG 0x1e 0
			WRITE_BYTE 0x29 0
		BUT_ONLY // No class or alignment restrictions, dex requirement retained.

		COPY_EXISTING ~NPMISC2.ITM~ ~override~ // Jan's gloves
			WRITE_LONG 0x1e 0x60B4FD80
			WRITE_BYTE 0x29 0
		BUT_ONLY // All thieves and bards allowed, dex requirement retained

		COPY_EXISTING ~NPPLAT.ITM~ ~override~ // Keldorn's armor
			WRITE_LONG 0x1e 0x604C0040
			WRITE_BYTE 0x29 0x70
			WRITE_BYTE 0x2a 0
			WRITE_BYTE 0x2b 0x03
			WRITE_BYTE 0x2d 0x80
			WRITE_BYTE 0x2e 0
			WRITE_BYTE 0x2f 0x04
			WRITE_BYTE 0x30 0
			WRITE_BYTE 0x32 0
		BUT_ONLY // Full plate usability

		COPY_EXISTING ~NPSHLD.ITM~ ~override~ // Anomen's shield
			WRITE_LONG 0x1e 0x604C0040
			WRITE_BYTE 0x29 0
			WRITE_BYTE 0x32 0
		BUT_ONLY // Medium shield usability, charisma requirement removed

		COPY_EXISTING ~NPSTAF.ITM~ ~override~ // Cernd's staff
			WRITE_LONG 0x1e 0x20000000
			WRITE_SHORT 0x26 5
			WRITE_BYTE 0x29 0
			WRITE_BYTE 0x2a 0
			WRITE_BYTE 0x2c 0
			WRITE_BYTE 0x2e 0
			WRITE_BYTE 0x30 0
			WRITE_BYTE 0x32 0
		BUT_ONLY // Quarterstaff usability

		COPY_EXISTING ~NPSW01.ITM~ ~override~ // Mazzy's sword
			WRITE_LONG 0x1e 0xDB84D780
			WRITE_BYTE 0x2b 0x02
		BUT_ONLY // Short sword usability plus halfling requirement

		COPY_EXISTING ~NPSW02.ITM~ ~override~ // Yoshimo's sword
			WRITE_LONG 0x1e 0x4004D780
			WRITE_BYTE 0x29 0
			WRITE_BYTE 0x2b 0x02
			WRITE_BYTE 0x2f 0
		BUT_ONLY // Katana usability

		COPY_EXISTING ~NPSW03.ITM~ ~override~ // Keldorn's sword
			WRITE_LONG 0x1e 0x604CD782
			WRITE_BYTE 0x29 0
			WRITE_BYTE 0x2b 0x02
			WRITE_BYTE 0x2f 0
			WRITE_BYTE 0x30 0
			WRITE_BYTE 0x32 0
		BUT_ONLY // Two-handed sword usability plus non-evil, Con and Cha reqs removed

		COPY_EXISTING ~NPSW04.ITM~ ~override~ // Valygar's sword
			WRITE_LONG 0x1e 0x4004D780
			WRITE_BYTE 0x29 0
			WRITE_BYTE 0x2b 0x02
			WRITE_BYTE 0x2d 0
		BUT_ONLY // Katana usability, attribute reqs retained

		COPY_EXISTING ~NPSW05.ITM~ ~override~ // Haer'Dalis' sword
			WRITE_LONG 0x1e 0x4004D780
			WRITE_BYTE 0x29 0
			WRITE_BYTE 0x2b 0x02
			WRITE_BYTE 0x2d 0
			WRITE_BYTE 0x2f 0
		BUT_ONLY // Short sword usability; Tiefling-only is an opcode 319

		COPY_EXISTING ~NPSW06.ITM~ ~override~ // Haer'Dalis' sword
			WRITE_LONG 0x1e 0x4004D780
			WRITE_BYTE 0x29 0
			WRITE_BYTE 0x2b 0x02
			WRITE_BYTE 0x2d 0
			WRITE_BYTE 0x2f 0
			LPF ADD_ITEM_EQEFFECT INT_VAR opcode=319 target=1 timing=2 parameter1=153 parameter2=4 power=1 special=8332 insert_point=4 END
		BUT_ONLY // Desc says tiefling-only, add that effect.

		COPY_EXISTING ~BOLT07.ITM~ ~override~ // Jan's Flashers
			WRITE_LONG 0x1e 0x4004D780
			WRITE_BYTE 0x29 0
		BUT_ONLY // Bolt usability, Int requirement retained
	END

//
//
// Green Slime poison can be cured
//
//

BEGIN @26000 DESIGNATED 260
GROUP @2
	COPY_EXISTING_REGEXP ".*\.ITM" ~override~ ".*\.SPL" ~override~
		LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=11 opcode=321 STR_VAR resource=JELLGR1 END
	BUT_ONLY // All poison cure effects now cure effects from Green Slime weapon

//
//
// Barrityl's Bigger and Better Burden
//
//

BEGIN @27000 DESIGNATED 270
GROUP @3
REQUIRE_PREDICATE GAME_IS ~bgee eet~ @13
	COPY ~jtweaks/resource/BARING.ITM~ ~override~

//
//
// Allies against Bodhi are better prepared for vampires
//
//

BEGIN @28000 DESIGNATED 280
GROUP @3
REQUIRE_PREDICATE GAME_IS ~bg2ee eet~ @11
	COPY_EXISTING ~C6ERIC.CRE~ ~override~
	              ~C6ERIC3.CRE~ ~override~
		WRITE_LONG 0x244 0x40060000
	BUT_ONLY // Eric Vanstraaten is an Undead Hunter
	EXTEND_TOP ~C6ERIC.BCS~ ~jtweaks/resource/C6EricPatch.baf~ // Apply UH passives

	COPY_EXISTING ~C6ARKAN.CRE~ ~override~
	              ~C6ARKAN3.CRE~ ~override~
		ADD_CRE_ITEM ~AMUL21~ #0 #0 #0 ~undroppable~ ~amulet~
	BUT_ONLY // Arkanis gets an undroppable Amulet of Power

	COPY_EXISTING ~AEGIS.ITM~ ~override~
	              ~AEGIS2.ITM~ ~override~
		READ_LONG 0x60 temp
		PATCH_IF (temp = 0) BEGIN
			WRITE_LONG 0x60 3
		END // Don't mess with the hammer if it's already been changed
	BUT_ONLY // Wulfgar's hammer can hit vampires

//
//
// Noober's Game
//
//

BEGIN @29000 DESIGNATED 290
GROUP @3
REQUIRE_PREDICATE GAME_IS ~bg2ee eet~ @11
	COPY ~jtweaks/resource/noober/J8#CSHRT.BAM~ ~override~ // Description pic
	COPY ~jtweaks/resource/noober/J8#ISHRT.BAM~ ~override~ // Inventory pic
	COPY ~jtweaks/resource/noober/J8#SHIRT.ITM~ ~override~ // The fabulous reward
		SAY NAME1 @29010
		SAY NAME2 @29010
		SAY UNIDENTIFIED_DESC @29011
		SAY DESC @29011
// Current graphics are placeholders, copied from base game resources
	COPY ~jtweaks/resource/noober/J8#SCRNO.ITM~ ~override~ // Summoning scroll
		SAY NAME2 @29020
		SAY DESC @29021
	COPY_EXISTING ~AMBAR01.STO~ ~override~
		ADD_STORE_ITEM ~J8#SCRNO~ LAST #0 #0 #0 ~IDENTIFIED~ #1
	BUT_ONLY
	COMPILE ~jtweaks/resource/noober/J8#NOOB.D~ // The main part
	COPY_EXISTING ~OHB6NOOB.CRE~ ~override/J8#NOOB.CRE~ // Noober
		WRITE_ASCII 0x280 ~J8#NOOB~ #32 // Script name
		WRITE_ASCII 0x2cc ~J8#NOOB~ #8 // Dialogue file
		WRITE_ASCII 0x248 ~INITDLG~ #8 // Override script
	BUT_ONLY
	EXTEND_BOTTOM ~BALDUR25.BCS~ ~jtweaks/resource/noober/NooberPatch.baf~ // Summon script

//
//
// Slightly improved party AI
//
//

BEGIN @30000 DESIGNATED 300
GROUP @4
ACTION_FOR_EACH script IN // Party combat AI scripts
	~BDAJANTC.BCS~ ~BDALORAC.BCS~ ~bdbaeloc.BCS~ ~BDBRANWC.BCS~ ~BDCORANC.BCS~ ~BDDEFAI.BCS~ ~bddornc.BCS~ ~bddynac.BCS~ ~bdedwinc.BCS~ ~BDELDOTC.BCS~ ~BDFALDOC.BCS~ ~BDGARRCC.BCS~ ~BDIMOENC.BCS~ ~bdjaheic.BCS~ ~BDKAGAIC.BCS~ ~bdkhalic.BCS~ ~BDKIVANC.BCS~ ~bdminscc.BCS~ ~BDMONTAC.BCS~ ~bdneerac.BCS~ ~BDQUALEC.BCS~ ~bdrasaac.BCS~ ~bdsafanc.BCS~ ~BDSHARTC.BCS~ ~BDSKIEC.BCS~ ~BDTIAXC.BCS~ ~bdviconc.BCS~ ~BDXANC.BCS~ ~BDXZARC.BCS~ ~BDYESLIC.BCS~ ~BDAERIEC.BCS~ ~BDANOMEC.BCS~ ~BDCERNDC.BCS~ ~BDHAERC.BCS~ ~BDHEXXAC.BCS~ ~BDJANC.BCS~ ~BDKELDOC.BCS~ ~BDKORGAC.BCS~ ~BDMAZZYC.BCS~ ~BDNALIAC.BCS~ ~BDSAREVC.BCS~ ~BDVALYGC.BCS~ ~BDWILSOC.BCS~ ~BDYOSHIC.BCS~ /* ~BDGLINTC.BCS~ ~BDCAELAC.BCS~ ~BDCORWIC.BCS~ ~BDGLINTC.BCS~ ~BDMKHIIC.BCS~ ~BDVOGHIC.BCS~ (SoD scripts excluded due to modder's lack of knowledge) */
	BEGIN
	ACTION_IF FILE_EXISTS_IN_GAME ~%script%~ THEN BEGIN
		COPY_EXISTING ~%script%~ ~override~
			REPLACE_BCS_BLOCK ~jtweaks/resource/combat-scripts/trapblock.baf~ ~jtweaks/resource/combat-scripts/empty.baf~ ON_MISMATCH END
			REPLACE_BCS_BLOCK ~jtweaks/resource/combat-scripts/hideblock1.baf~ ~jtweaks/resource/combat-scripts/empty.baf~ ON_MISMATCH END
			REPLACE_BCS_BLOCK ~jtweaks/resource/combat-scripts/hideblock2.baf~ ~jtweaks/resource/combat-scripts/empty.baf~ ON_MISMATCH END
			REPLACE_BCS_BLOCK ~jtweaks/resource/combat-scripts/singblock1.baf~ ~jtweaks/resource/combat-scripts/empty.baf~ ON_MISMATCH END
			REPLACE_BCS_BLOCK ~jtweaks/resource/combat-scripts/singblock2.baf~ ~jtweaks/resource/combat-scripts/empty.baf~ ON_MISMATCH END
			REPLACE_BCS_BLOCK ~jtweaks/resource/combat-scripts/singblock3.baf~ ~jtweaks/resource/combat-scripts/empty.baf~ ON_MISMATCH END
			REPLACE_BCS_BLOCK ~jtweaks/resource/combat-scripts/stopsingblock1.baf~ ~jtweaks/resource/combat-scripts/empty.baf~ ON_MISMATCH END
			REPLACE_BCS_BLOCK ~jtweaks/resource/combat-scripts/stopsingblock2.baf~ ~jtweaks/resource/combat-scripts/empty.baf~ ON_MISMATCH END
			REPLACE_BCS_BLOCK ~jtweaks/resource/combat-scripts/turnblock.baf~ ~jtweaks/resource/combat-scripts/empty.baf~ ON_MISMATCH END
			REPLACE_BCS_BLOCK ~jtweaks/resource/combat-scripts/attackblock1.baf~ ~jtweaks/resource/combat-scripts/CombatAIPatch.baf~ ON_MISMATCH END
			REPLACE_BCS_BLOCK ~jtweaks/resource/combat-scripts/attackblock2.baf~ ~jtweaks/resource/combat-scripts/CombatAIPatch.baf~ ON_MISMATCH END
			REPLACE_BCS_BLOCK ~jtweaks/resource/combat-scripts/attackblock3.baf~ ~jtweaks/resource/combat-scripts/CombatAIPatch.baf~ ON_MISMATCH END
			REPLACE_BCS_BLOCK ~jtweaks/resource/combat-scripts/attackblock4.baf~ ~jtweaks/resource/combat-scripts/CombatAIPatch.baf~ ON_MISMATCH END
			REPLACE_BCS_BLOCK ~jtweaks/resource/combat-scripts/attackblock5.baf~ ~jtweaks/resource/combat-scripts/CombatAIPatch.baf~ ON_MISMATCH END
		BUT_ONLY END
	END

//
//
// Map notes for BGEE quests
//
//

BEGIN @31000 DESIGNATED 310
GROUP @5
	REQUIRE_PREDICATE GAME_IS ~bgee eet~ @13
	ACTION_IF GAME_IS ~bgee~ THEN BEGIN
		OUTER_SPRINT baldurNW AR0100
		OUTER_SPRINT baldurNC AR0200
		OUTER_SPRINT baldurCW AR0600
		OUTER_SPRINT baldurCC AR0700
		OUTER_SPRINT baldurCE AR0800
		OUTER_SPRINT baldurSC AR1200
		OUTER_SPRINT baldurSE AR1300
		OUTER_SPRINT frarminn AR2300
		OUTER_SPRINT beregost AR3300
		OUTER_SPRINT nashkel AR4800
	END ELSE BEGIN
		OUTER_SPRINT baldurNW BG0100
		OUTER_SPRINT baldurNC BG0200
		OUTER_SPRINT baldurCW BG0600
		OUTER_SPRINT baldurCC BG0700
		OUTER_SPRINT baldurCE BG0800
		OUTER_SPRINT baldurSC BG1200
		OUTER_SPRINT baldurSE BG1300
		OUTER_SPRINT frarminn AR2300
		OUTER_SPRINT beregost BG3300
		OUTER_SPRINT nashkel BG4800
	END // Area codes for EET compatibility

	OUTER_SET PheirkasNote = RESOLVE_STR_REF (@31010)
	OUTER_SET QuinnNote = 0 // Default value in case it's missing from the area file
	COPY_EXISTING ~%baldurNW%.ARE~ ~override~
		READ_LONG 0xC4 ofsNotes
		READ_LONG 0xC8 numNotes
		FOR (n=0; n<numNotes; ++n) BEGIN
			SET ofsX = ofsNotes + (n*52)
			SET ofsY = ofsNotes + (n*52) + 2
			SET ofsText = ofsNotes + (n*0x34) + 4
			READ_SHORT ofsX xpos
			READ_SHORT ofsY ypos
			PATCH_IF ((xpos=3639) AND (ypos=2271)) BEGIN
				READ_LONG ofsText noteRef
				SET QuinnNote = %noteRef%
			END
		END // Get existing string reference for "Quinn's House" note
	BUT_ONLY
	ACTION_IF (QuinnNote = 0) BEGIN
		OUTER_SET QuinnNote = RESOLVE_STR_REF (@31011)
	END // Backup plan for if the map note has already been removed
	EXTEND_BOTTOM ~%baldurNW%.BCS~ ~jtweaks/resource/map-notes/BaldurNWPatch.baf~ EVALUATE_BUFFER
	
	OUTER_SET BherenNote = RESOLVE_STR_REF (@31020)
	OUTER_SET RinnieNote = RESOLVE_STR_REF (@31021)
	EXTEND_BOTTOM ~%baldurNC%.BCS~ ~jtweaks/resource/map-notes/BaldurNCPatch.baf~ EVALUATE_BUFFER

	OUTER_SET FentenNote = RESOLVE_STR_REF (@31060)
	OUTER_SET TremainNote = RESOLVE_STR_REF (@31061)
	EXTEND_BOTTOM ~%baldurCW%.BCS~ ~jtweaks/resource/map-notes/BaldurCWPatch.baf~ EVALUATE_BUFFER

	OUTER_SET FeloniusNote = 0
	OUTER_SET NadineNote = RESOLVE_STR_REF (@31071)
	OUTER_SET OberanNote = 0
	COPY_EXISTING ~%baldurCC%.ARE~ ~override~
		READ_LONG 0xC4 ofsNotes
		READ_LONG 0xC8 numNotes
		FOR (n=0; n<numNotes; ++n) BEGIN
			SET ofsX = ofsNotes + (n*52)
			SET ofsY = ofsNotes + (n*52) + 2
			SET ofsText = ofsNotes + (n*0x34) + 4
			READ_SHORT ofsX xpos
			READ_SHORT ofsY ypos
			PATCH_IF ((xpos=2003) AND (ypos=732)) BEGIN
				READ_LONG ofsText noteRef
				SET FeloniusNote = %noteRef%
			END
			PATCH_IF ((xpos=3446) AND (ypos=176)) BEGIN
				READ_LONG ofsText noteRef
				SET OberanNote = %noteRef%
			END
		END // Get existing string references
	BUT_ONLY
	ACTION_IF (FeloniusNote = 0) BEGIN
		OUTER_SET QuinnNote = RESOLVE_STR_REF (@31070)
	END
	ACTION_IF (OberanNote = 0) BEGIN
		OUTER_SET QuinnNote = RESOLVE_STR_REF (@31072)
	END
	EXTEND_BOTTOM ~%baldurCC%.BCS~ ~jtweaks/resource/map-notes/BaldurCCPatch.baf~ EVALUATE_BUFFER

	OUTER_SET ArkionNote = RESOLVE_STR_REF (@31080)
	OUTER_SET ThiefGuildNote = 0
	COPY_EXISTING ~%baldurCE%.ARE~ ~override~
		READ_LONG 0xC4 ofsNotes
		READ_LONG 0xC8 numNotes
		FOR (n=0; n<numNotes; ++n) BEGIN
			SET ofsX = ofsNotes + (n*52)
			SET ofsY = ofsNotes + (n*52) + 2
			SET ofsText = ofsNotes + (n*0x34) + 4
			READ_SHORT ofsX xpos
			READ_SHORT ofsY ypos
			PATCH_IF ((xpos=1150) AND (ypos=1254)) BEGIN
				READ_LONG ofsText noteRef
				SET ThiefGuildNote = %noteRef%
			END
		END // Get existing string reference for "Thieves' Guild Entrance" note
	BUT_ONLY
	ACTION_IF (ThiefGuildNote = 0) BEGIN
		OUTER_SET ThiefGuildNote = RESOLVE_STR_REF (@31081)
	END
	EXTEND_BOTTOM ~%baldurCE%.BCS~ ~jtweaks/resource/map-notes/BaldurCEPatch.baf~ EVALUATE_BUFFER

	OUTER_SET CordyrNote = RESOLVE_STR_REF (@31120)
	OUTER_SET GhorakNote = RESOLVE_STR_REF (@31121)
	EXTEND_BOTTOM ~%baldurSC%.BCS~ ~jtweaks/resource/map-notes/BaldurSCPatch.baf~ EVALUATE_BUFFER

	OUTER_SET GantolandanNote = 0
	OUTER_SET NemphreNote = RESOLVE_STR_REF (@31131)
	COPY_EXISTING ~%baldurSE%.ARE~ ~override~
		READ_LONG 0xC4 ofsNotes
		READ_LONG 0xC8 numNotes
		FOR (n=0; n<numNotes; ++n) BEGIN
			SET ofsX = ofsNotes + (n*52)
			SET ofsY = ofsNotes + (n*52) + 2
			SET ofsText = ofsNotes + (n*0x34) + 4
			READ_SHORT ofsX xpos
			READ_SHORT ofsY ypos
			PATCH_IF ((xpos=742) AND (ypos=1257)) BEGIN
				READ_LONG ofsText noteRef
				SET GantolandanNote = %noteRef%
			END
		END // Get existing string reference for "Gantolandan's House" note
	BUT_ONLY
	ACTION_IF (GantolandanNote = 0) BEGIN
		OUTER_SET GantolandanNote = RESOLVE_STR_REF (@31130)
	END
	EXTEND_BOTTOM ~%baldurSE%.BCS~ ~jtweaks/resource/map-notes/BaldurSEPatch.baf~ EVALUATE_BUFFER

	OUTER_SET JoiaNote = RESOLVE_STR_REF (@31230)
	EXTEND_BOTTOM ~%frarminn%.BCS~ ~jtweaks/resource/map-notes/FrArmInnPatch.baf~ EVALUATE_BUFFER

	OUTER_SET ColquetleNote = RESOLVE_STR_REF (@31330)
	OUTER_SET LandrinNote = 0
	OUTER_SET MirianneNote = RESOLVE_STR_REF (@31332)
	COPY_EXISTING ~%beregost%.ARE~ ~override~
		READ_LONG 0xC4 ofsNotes
		READ_LONG 0xC8 numNotes
		FOR (n=0; n<numNotes; ++n) BEGIN
			SET ofsX = ofsNotes + (n*52)
			SET ofsY = ofsNotes + (n*52) + 2
			SET ofsText = ofsNotes + (n*0x34) + 4
			READ_SHORT ofsX xpos
			READ_SHORT ofsY ypos
			PATCH_IF ((xpos=3059) AND (ypos=3468)) BEGIN
				READ_LONG ofsText noteRef
				SET LandrinNote = %noteRef%
			END
		END // Get existing string reference for "Landrin's House" note
	BUT_ONLY
	ACTION_IF (LandrinNote = 0) BEGIN
		OUTER_SET LandrinNote = RESOLVE_STR_REF (@31331)
	END
	EXTEND_BOTTOM ~%beregost%.BCS~ ~jtweaks/resource/map-notes/BeregostPatch.baf~ EVALUATE_BUFFER

	OUTER_SET JosephNote = RESOLVE_STR_REF (@31480)
	EXTEND_BOTTOM ~%nashkel%.BCS~ ~jtweaks/resource/map-notes/NashkelPatch.baf~ EVALUATE_BUFFER

	COMPILE ~jtweaks/resource/map-notes/MapNotePatch.d~ // Add dialogue actions

//
//
// Standardize inn and tavern music
//
//

BEGIN @32000 DESIGNATED 320
GROUP @5
	REQUIRE_PREDICATE GAME_IS ~bgee eet~ @13
	ACTION_IF GAME_IS ~bgee~ THEN BEGIN
		OUTER_SPRINT elfsong1 AR0705
		OUTER_SPRINT elfsong2 AR0706
		OUTER_SPRINT ulgoth1 AR1001
		OUTER_SPRINT yeolde2 AR1114
		OUTER_SPRINT lowlant1 AR0133
		OUTER_SPRINT lowlant2 AR0134
		OUTER_SPRINT lowlant3 AR0135
		OUTER_SPRINT lowlant4 AR0136
		OUTER_SPRINT inntav2 AR1307
		OUTER_SPRINT friarm2 AR2302
		OUTER_SPRINT friarm3 AR2303
	END ELSE BEGIN
		OUTER_SPRINT elfsong1 BG0705
		OUTER_SPRINT elfsong2 BG0706
		OUTER_SPRINT ulgoth1 BG1001
		OUTER_SPRINT yeolde2 BG1114
		OUTER_SPRINT lowlant1 BG0133
		OUTER_SPRINT lowlant2 BG0134
		OUTER_SPRINT lowlant3 BG0135
		OUTER_SPRINT lowlant4 BG0136
		OUTER_SPRINT inntav2 BG1307
		OUTER_SPRINT friarm2 BG2302
		OUTER_SPRINT friarm3 BG2303
	END // Area codes for EET compatibility
	OUTER_SET songtav2 = IDS_OF_SYMBOL (~songlist~ ~tav2~)
	OUTER_SET songtav4 = IDS_OF_SYMBOL (~songlist~ ~tav4~)
	// 36 and 38 in BGEE, 
	// BG1 and BG2 versions of these songs differ. Same music, added breaks in BG2.

	COPY_EXISTING ~%yeolde2%.ARE~ ~override~
	              ~%lowlant3%.ARE~ ~override~
	              ~%lowlant4%.ARE~ ~override~
	              ~%inntav2%.ARE~ ~override~
	              ~%friarm2%.ARE~ ~override~
	              ~%friarm3%.ARE~ ~override~
		READ_LONG 0xBC ofsSong
		SET ofsDay = ofsSong
		SET ofsNight = ofsSong + 4
		WRITE_LONG ofsDay 0xFFFFFFFF
		WRITE_LONG ofsNight 0xFFFFFFFF
	BUT_ONLY // Areas with "no" music.

	COPY_EXISTING ~%lowlant1%.ARE~ ~override~
	              ~%lowlant2%.ARE~ ~override~
		READ_LONG 0xBC ofsSong
		SET ofsDay = ofsSong
		SET ofsNight = ofsSong + 4
		WRITE_LONG ofsDay %songtav2%
		WRITE_LONG ofsNight %songtav2%
	BUT_ONLY // Areas with tavern music #2. I rolled a die to choose.
	COPY_EXISTING ~%elfsong1%.ARE~ ~override~
	              ~%elfsong2%.ARE~ ~override~
	              ~%ulgoth1%.ARE~ ~override~
		READ_LONG 0xBC ofsSong
		SET ofsDay = ofsSong
		SET ofsNight = ofsSong + 4
		WRITE_LONG ofsDay %songtav4%
		WRITE_LONG ofsNight %songtav4%
	BUT_ONLY // Areas with tavern music #4.

//
//
// Randomize battle music
//
//

BEGIN @33000 DESIGNATED 330
GROUP @5
	ACTION_IF (FILE_EXISTS_IN_GAME ~TU0018.ARE~) BEGIN
		COPY_EXISTING ~TU0018.ARE~ ~override~
			SET songid = IDS_OF_SYMBOL (~songlist~ ~BD1~)
			READ_LONG 0xBC ofsSong
			WRITE_LONG (ofsSong + 12) songid
		BUT_ONLY
	END // Pre-processing. Battle song unused in BGEE songlist, valid in BG2EE.
	ACTION_IF (GAME_IS ~bgee eet~) BEGIN
		ACTION_IF GAME_IS ~bgee~ BEGIN
			OUTER_SPRINT ckcata1 ~AR2615~
			OUTER_SPRINT bgsewere ~AR0226~
		END ELSE BEGIN
			OUTER_SPRINT ckcata1 ~BG2615~
			OUTER_SPRINT bgsewere ~BG0226~
		END
		COPY_EXISTING ~%ckcata1%.ARE~ ~override~
		              ~%bgsewere%.ARE~ ~override~
			READ_LONG 0xBC ofsSong
			WRITE_LONG (ofsSong + 12) 0
		BUT_ONLY
	END // More BGEE pre-processing. Unsuitable music removed.
	ACTION_IF (GAME_IS ~bg2ee eet~) BEGIN
		COPY_EXISTING ~AR0014.ARE~ ~override~
		              ~AR0021.ARE~ ~override~
		              ~AR0028.ARE~ ~override~
		              ~AR0069.ARE~ ~override~
		              ~AR0070.ARE~ ~override~
		              ~AR0071.ARE~ ~override~
		              ~AR0072.ARE~ ~override~
		              ~AR0083.ARE~ ~override~
		              ~AR0084.ARE~ ~override~
		              ~AR5012.ARE~ ~override~
			READ_LONG 0xBC ofsSong
			WRITE_LONG (ofsSong + 12) 0
		BUT_ONLY
	END // BG2EE pre-processing. Unsuitable music removed. Mostly cutscene areas.
	ACTION_CLEAR_ARRAY battlemusic
	OUTER_SET bsnum = 0
	ACTION_FOR_EACH songname IN ~BC1~ ~BC2~ ~BD1~ ~BD2~ ~BD3~ ~BF1~ ~BF2~ ~BJR~ ~BL1~ ~BL2~ ~BM1~ ~BM2~ ~BP1~ ~BP2~ ~BPC1~ ~BPC2~ ~BPC3~ ~BRD~ ~BSD~ ~BST~ ~BW1~ ~Dorn1~ ~Mb~ ~Neera1~ ~Neera2~ ~Vb~ BEGIN
		OUTER_SET songid = IDS_OF_SYMBOL (~songlist~ ~%songname%~)
		ACTION_IF (songid > 0) BEGIN
			OUTER_SET bsnum += 1
			OUTER_SET $battlemusic(~%bsnum%~) = songid
		END
	END // Build initial list of songs. Full coverage for non-SoD BGEE and BG2EE

	ACTION_IF (debug_messages) BEGIN
		OUTER_SET idscount = bsnum
		PRINT ~%idscount% songs found in IDS list~
	END

	COPY_EXISTING_REGEXP GLOB ~^.+\.ARE$~ ~override~
		READ_LONG 0xBC ofsSong
		READ_LONG (ofsSong + 12) songid
		SET AlreadyThere = 0
		PHP_EACH battlemusic AS n => songref BEGIN
			PATCH_IF (songid = songref) BEGIN
				SET AlreadyThere = 1
			END
		END
		PATCH_IF ((songid != 0) AND (AlreadyThere = 0)) BEGIN
			PATCH_IF (RESOURCE_CONTAINS songlist.IDS ~%songid% ~) BEGIN
				SET bsnum += 1
				SET $battlemusic(~%bsnum%~) = songid
				PATCH_IF (debug_messages) BEGIN
					PATCH_PRINT ~Song %songid% added from area %SOURCE_RES%~
				END
			END // Add any song I missed above, if it's in songlist.
		END // But not the blank song at ID zero
	BUT_ONLY

	ACTION_IF (debug_messages) BEGIN
		OUTER_SET areacount = %bsnum% - %idscount%
		PRINT ~%areacount% additional songs found used by areas~ 
	END

	COPY_EXISTING_REGEXP GLOB ~^.+\.ARE$~ ~override~
		READ_LONG 0xBC ofsSong
		SET ofsBattle = ofsSong + 12
		SET randIndex = RANDOM (1 bsnum)
		SET randSong = $battlemusic(~%randIndex%~)
		WRITE_LONG ofsBattle randSong
	BUT_ONLY // Make the changes.
	
//
//
// Potion effects don't stack with themselves
//
//

BEGIN @34000 DESIGNATED 340
GROUP @2
	COPY_EXISTING_REGEXP GLOB ~^.+\.ITM$~ ~override~
		READ_SHORT 0x1c itemtype
		PATCH_IF (itemtype = 9) BEGIN
			LPF CLONE_EFFECT INT_VAR check_globals=0 header_type=3 multi_match=1 silent=1 match_target=1 match_timing=0 opcode=321 power=0 parameter1=0 parameter2=0 timing=1 resist_dispel=2 duration=0 probability1=100 probability2=0 dicenumber=0 dicesize=0 savingthrow=0 savebonus=0 special=0 STR_VAR insert=first resource=~%SOURCE_RES%~ END
		END // If it's a potion with at least one self-targeted temporary effect,
	BUT_ONLY // make it so the first thing it does is remove its own effects.

//
//
// Bigger random spawns
//
//

BEGIN @35000 DESIGNATED 350
GROUP @3
	ACTION_CLEAR_ARRAY spawnGroups
	OUTER_SET NumSpawnGroups = 0
	COPY_EXISTING ~SPAWNGRP.2DA~ ~override~
		COUNT_2DA_COLS cols
		SET NumSpawnGroups = cols - 1
		READ_2DA_ENTRIES_NOW rows NumSpawnGroups // We only want the header row.
		FOR (n = 0; n < NumSpawnGroups; ++n) BEGIN
			READ_2DA_ENTRY_FORMER rows 0 n temp
			SPRINT $spawnGroups(~%n%~) ~%temp%~
		END
	BUT_ONLY // No change to the 2da. We merely read it to build an array.

	COPY_EXISTING_REGEXP GLOB ~^.+\.ARE$~ ~override~
		READ_LONG 0x60 ofsSpawn
		READ_LONG 0x64 numSpawn
		FOR (k = 0; k < numSpawn; ++k) BEGIN
			SET isMonGrp = 0
			SET ofsNumCre = ofsSpawn + 200*k + 116
			READ_SHORT ofsNumCre NumCre
			FOR (j = 1; j <= NumCre; ++j) BEGIN
				SET ofsCre = ofsSpawn + 200*k + 28 + 8*j
				READ_ASCII ofsCre creName (8) NULL
				PHP_EACH spawnGroups AS index => monGrp BEGIN
					PATCH_IF (~%monGrp%~ STR_EQ ~%creName%~) BEGIN
						SET isMonGrp = 1
					END
				END // Check if it's a group from SPAWNGRP.2DA
			END
			PATCH_IF (isMonGrp = 0) BEGIN
				SET ofsMax = ofsSpawn + 200*k + 132
				WRITE_SHORT ofsMax 10
			END // The actual change
		END
	BUT_ONLY

//
//
// Nature's Beauty blindness can be cured
//
//

BEGIN @36000 DESIGNATED 360
GROUP @1
	COPY_EXISTING_REGEXP_GLOB ~^.+\.SPL$~ ~override~
		LPF ALTER_EFFECT INT_VAR silent=1 match_opcode=74 match_timing=1 timing=0 duration=30000000 END
		LPF ALTER_EFFECT INT_VAR silent=1 match_opcode=142 match_parameter2=8 match_timing=1 timing=0 duration=30000000 END
	BUT_ONLY // Change broken permanent effect to long-duration temporary.